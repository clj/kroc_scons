-- version control marker $Id: cg10i32.occ,v 1.1 1995/10/10 13:02:55 djb1 Exp $
PROC do.INT32 ()
  INT32 a, b, c :
  SEQ
    b := id.INT32(85 (INT32))
    --{{{  output message
    title ("INT32 arithmetic")
    --}}} 
    --{{{  check arithmetic and logical operators  (I32-000 to 099)
    a := b + 23 (INT32)
    check.INT32 (a, 85(INT32) + 23(INT32), "I32-001")
    a := b PLUS 23 (INT32)
    check.INT32 (a, 85(INT32) PLUS 23(INT32), "I32-002")
    a := b - 23 (INT32)
    check.INT32 (a, 85(INT32) - 23(INT32), "I32-003")
    a := b MINUS 23 (INT32)
    check.INT32 (a, 85(INT32) MINUS 23(INT32), "I32-004")
    a := b * 23 (INT32)
    check.INT32 (a, 85(INT32) * 23(INT32), "I32-005")
    a := b TIMES 23 (INT32)
    check.INT32 (a, 85(INT32) TIMES 23(INT32), "I32-006")
    a := b / 23 (INT32)
    check.INT32 (a, 85(INT32) / 23(INT32), "I32-007")
    a := b \ 23 (INT32)
    check.INT32 (a, 85(INT32) \ 23(INT32), "I32-008")
    --{{{  check logical operators                 (I32-009 to 014)
    a := b << 3
    check.INT32 (a, 85(INT32) << 3, "I32-009")
    a := b >> 3
    check.INT32 (a, 85(INT32) >> 3, "I32-010")
    a := b /\ 23 (INT32)
    check.INT32 (a, 85(INT32) /\ 23(INT32), "I32-011")
    a := b \/ 23 (INT32)
    check.INT32 (a, 85(INT32) \/ 23(INT32), "I32-012")
    a := b >< 23 (INT32)
    check.INT32 (a, 85(INT32) >< 23(INT32), "I32-013")
    a := ~b
    check.INT32 (a, ~85(INT32), "I32-014")
    --}}} 
    --{{{  unary minus                             (I32-015 to 018)
    a := (- b)
    check.INT32 (a, -85(INT32), "I32-015")
    a := (MINUS b)
    check.INT32 (a, MINUS 85(INT32), "I32-016")
    --{{{  
    a := MINUS (MOSTNEG INT32)
    check.INT32 (a, MOSTNEG INT32, "I32-017")
    --}}} 
    --{{{  
    c := MOSTNEG INT32
    a := MINUS c
    check.INT32 (a, c, "I32-018")
    --}}} 
    --}}} 
    --{{{  sign extension                          (I32-019 to 029)
    PROC extension()
      PROC local(VAL INT32 w, x, y, z, INT i)
        i := ((INT w) + (INT x)) + ((INT y) + (INT z))
      :
      INT i :
      SEQ
        local(-1(INT32), -1(INT32), -1(INT32), -1(INT32), i)
        check(i, -4, "I32-019")
        local(1(INT32), 1(INT32), 1(INT32), 1(INT32), i)
        check(i, 4, "I32-020")
    :
    extension()
    --}}}
    --{{{  overflow checks on logical operators    (I32-030 to 039)
    INT32 p, q, r :
    SEQ
      p := id.INT32(MOSTPOS INT32)
      q := p PLUS 256(INT32)
      check.INT32(q, (MOSTNEG INT32) + 255(INT32), "I32-030")
    
      p := id.INT32(MOSTNEG INT32)
      q := p PLUS (-256(INT32))
      check.INT32(q, (MOSTPOS INT32) - 255(INT32), "I32-031")
    
      p := id.INT32(MOSTPOS INT32)
      q := p MINUS (-256(INT32))
      check.INT32(q, (MOSTNEG INT32) + 255(INT32), "I32-032")
    
      p := id.INT32(MOSTNEG INT32)
      q := p MINUS 256(INT32)
      check.INT32(q, (MOSTPOS INT32) - 255(INT32), "I32-033")
    
      p := id.INT32(MOSTPOS INT32)
      q := p TIMES 2(INT32)
      check.INT32(q, (MOSTPOS INT32) TIMES 2(INT32), "I32-034")
      check.INT32(q, (MOSTPOS INT32) << 1,           "I32-035")
    
      p := id.INT32(MOSTPOS INT32)
      q := MINUS p
      check.INT32(q, 0(INT32) MINUS (MOSTPOS INT32), "I32-036")
      check.INT32(q, (MOSTNEG INT32) + 1(INT32),     "I32-037")
    
      r := id.INT32(0(INT32))
      p := id.INT32(MOSTPOS INT32)
      q := (p PLUS 256(INT32)) + r
      check.INT32(q, (MOSTNEG INT32) + 255(INT32), "I32-038")
    
    --}}}
    
    --}}} 
    --{{{  check constant folding                  (I32-100 to 199)
    INT32 a, b :
    INT c :
    SEQ
      b := id.INT32(5 (INT32))
      c := 3
      --{{{  +        (I32-100)
      a := 2 (INT32) + b               -- 7
      a := (1 (INT32) + b    ) + a     -- 6
      a := (0 (INT32) + b    ) + a     -- 5
      a := ((-1 (INT32)) + b ) + a     -- 4
      a := ((-2 (INT32)) + b ) + a     -- 3
      a := (b + 2 (INT32)    ) + a     -- 7
      a := (b + 1 (INT32)    ) + a     -- 6
      a := (b + 0 (INT32)    ) + a     -- 5
      a := (b + (-1 (INT32)) ) + a     -- 4
      a := (b + (-2 (INT32)) ) + a     -- 3
      check.INT32 (a, 50 (INT32), "I32-100")
      --}}} 
      --{{{  -        (I32-101)
      a := 2 (INT32) - b               --  -3
      a := (1 (INT32) - b    ) + a     --  -4
      a := (0 (INT32) - b    ) + a     --  -5
      a := ((-1 (INT32)) - b ) + a     --  -6
      a := ((-2 (INT32)) - b ) + a     --  -7
      a := (b - 2 (INT32)    ) + a     --   3
      a := (b - 1 (INT32)    ) + a     --   4
      a := (b - 0 (INT32)    ) + a     --   5
      a := (b - (-1 (INT32)) ) + a     --   6
      a := (b - (-2 (INT32)) ) + a     --   7
      check.INT32 (a, 0 (INT32), "I32-101")
      --}}} 
      --{{{  *        (I32-102)
      a := 2 (INT32) * b                 --  10
      a := (1 (INT32) * b      ) + a     --   5
      a := (0 (INT32) * b      ) + a     --   0
      a := ((-1 (INT32)) * b   ) + a     --  -5
      a := ((-2 (INT32)) * b   ) + a     -- -10
      a := (b * 2 (INT32)      ) + a     --  10
      a := (b * 1 (INT32)      ) + a     --   5
      a := (b * 0 (INT32)      ) + a     --   0
      a := (b * (-1 (INT32))   ) + a     --  -5
      a := (b * (-2 (INT32))   ) + a     -- -10
      check.INT32 (a, 0 (INT32), "I32-102")
      --}}} 
      --{{{  /        (I32-103)
      a := 2 (INT32) / b                 -- 0
      a := (1 (INT32) / b      ) + a     -- 0
      a := (0 (INT32) / b      ) + a     -- 0
      a := ((-1 (INT32)) / b   ) + a     -- 0
      a := ((-2 (INT32)) / b   ) + a     -- 0
      a := (b / 2 (INT32)      ) + a     -- 2
      a := (b / 1 (INT32)      ) + a     -- 5
      --a := b / 0 (INT32)  --should give error
      a := (b / (-1 (INT32))   ) + a     -- -5
      a := (b / (-2 (INT32))   ) + a     -- -2
      check.INT32 (a, 0 (INT32), "I32-103")
      --}}} 
      --{{{  \        (I32-104)
      a := 2 (INT32) \ b                 --  2
      a := (1 (INT32) \ b      ) + a     --  1
      a := (0 (INT32) \ b      ) + a     --  0
      a := ((-1 (INT32)) \ b   ) + a     -- -1
      a := ((-2 (INT32)) \ b   ) + a     -- -2
      a := (b \ 2 (INT32)      ) + a     --  1
      a := (b \ 1 (INT32)      ) + a     --  0
      --a := b \ 0 (INT32)  --should give error
      a := (b \ (-1 (INT32))   ) + a     --  0
      a := (b \ (-2 (INT32))   ) + a     --  1
      check.INT32 (a, 2 (INT32), "I32-104")
      --}}} 
      --{{{  PLUS     (I32-105)
      a := 2 (INT32) PLUS b                 -- 7
      a := (1 (INT32) PLUS b      ) + a     -- 6
      a := (0 (INT32) PLUS b      ) + a     -- 5
      a := ((-1 (INT32)) PLUS b   ) + a     -- 4
      a := ((-2 (INT32)) PLUS b   ) + a     -- 3
      a := (b PLUS 2 (INT32)      ) + a     -- 7
      a := (b PLUS 1 (INT32)      ) + a     -- 6
      a := (b PLUS 0 (INT32)      ) + a     -- 5
      a := (b PLUS (-1 (INT32))   ) + a     -- 4
      a := (b PLUS (-2 (INT32))   ) + a     -- 3
      check.INT32 (a, 50 (INT32), "I32-105")
      --}}} 
      --{{{  MINUS    (I32-106)
      a := 2 (INT32) MINUS b                  --  -3
      a := (1 (INT32) MINUS b       ) + a     --  -4
      a := (0 (INT32) MINUS b       ) + a     --  -5
      a := ((-1 (INT32)) MINUS b    ) + a     --  -6
      a := ((-2 (INT32)) MINUS b    ) + a     --  -7
      a := (b MINUS 2 (INT32)       ) + a     --   3
      a := (b MINUS 1 (INT32)       ) + a     --   4
      a := (b MINUS 0 (INT32)       ) + a     --   5
      a := (b MINUS (-1 (INT32))    ) + a     --   6
      a := (b MINUS (-2 (INT32))    ) + a     --   7
      check.INT32 (a, 0 (INT32), "I32-106")
      --}}} 
      --{{{  TIMES    (I32-107)
      a := 2 (INT32) TIMES b                --  10
      a := (1 (INT32) TIMES b     ) + a     --   5
      a := (0 (INT32) TIMES b     ) + a     --   0
      a := ((-1 (INT32)) TIMES b  ) + a     --  -5
      a := ((-2 (INT32)) TIMES b  ) + a     -- -10
      a := (b TIMES 2 (INT32)     ) + a     --  10
      a := (b TIMES 1 (INT32)     ) + a     --   5
      a := (b TIMES 0 (INT32)     ) + a     --   0
      a := (b TIMES (-1 (INT32))  ) + a     --  -5
      a := (b TIMES (-2 (INT32))  ) + a     -- -10
      check.INT32 (a, 0 (INT32), "I32-107")
      --}}} 
      --{{{  /\       (I32-108)
      a := 2 (INT32) /\ b                   -- 0
      a := (1 (INT32) /\ b        ) + a     -- 1
      a := (0 (INT32) /\ b        ) + a     -- 0
      a := ((-1 (INT32)) /\ b     ) + a     -- 5
      a := ((-2 (INT32)) /\ b     ) + a     -- 4
      a := (b /\ 2 (INT32)        ) + a     -- 0
      a := (b /\ 1 (INT32)        ) + a     -- 1
      a := (b /\ 0 (INT32)        ) + a     -- 0
      a := (b /\ (-1 (INT32))     ) + a     -- 5
      a := (b /\ (-2 (INT32))     ) + a     -- 4
      check.INT32 (a, 20 (INT32), "I32-108")
      a := b /\ 0(INT32)
      check.INT32 (a, 0(INT32), "I32-108a")
      a := b /\ (-1(INT32))
      check.INT32 (a, 5(INT32), "I32-108b")
      a := b /\ b
      check.INT32 (a, 5(INT32), "I32-108c")
      INT32 bb :
      SEQ
        bb := id.INT32(#0101FF05(INT32))
        a := bb /\ 0(INT32)
        check.INT32 (a, 0(INT32), "I32-108d")
        a := bb /\ (-1(INT32))
        check.INT32 (a, #0101FF05(INT32), "I32-108e")
        a := bb /\ #0101FF05(INT32)
        check.INT32 (a, #0101FF05(INT32), "I32-108f")
        a := bb /\ #0000FFFF(INT32)
        check.INT32 (a, #0000FF05(INT32), "I32-108g")
        a := bb /\ #FFFF0000(INT32)
        check.INT32 (a, #01010000(INT32), "I32-108h")
      
      --}}} 
      --{{{  \/       (I32-109)
      a := 2 (INT32) \/ b                 --  7
      a := (1 (INT32) \/ b      ) + a     --  5
      a := (0 (INT32) \/ b      ) + a     --  5
      a := ((-1 (INT32)) \/ b   ) + a     -- -1
      a := ((-2 (INT32)) \/ b   ) + a     -- -1
      a := (b \/ 2 (INT32)      ) + a     --  7
      a := (b \/ 1 (INT32)      ) + a     --  5
      a := (b \/ 0 (INT32)      ) + a     --  5
      a := (b \/ (-1 (INT32))   ) + a     -- -1
      a := (b \/ (-2 (INT32))   ) + a     -- -1
      check.INT32 (a, 30 (INT32), "I32-109")
      a := b \/ 0(INT32)
      check.INT32 (a, 5(INT32), "I32-109a")
      a := b \/ (-1(INT32))
      check.INT32 (a, -1(INT32), "I32-109b")
      a := b \/ b
      check.INT32 (a, 5(INT32), "I32-109c")
      INT32 bb :
      SEQ
        bb := id.INT32(#0101FF05(INT32))
        a := bb \/ 0(INT32)
        check.INT32 (a, #0101FF05(INT32), "I32-109d")
        a := bb \/ (-1(INT32))
        check.INT32 (a, -1(INT32), "I32-109e")
        a := bb \/ #0101FF05(INT32)
        check.INT32 (a, #0101FF05(INT32), "I32-109f")
        a := bb \/ #0000FFFF(INT32)
        check.INT32 (a, #0101FFFF(INT32), "I32-109g")
        a := bb \/ #FFFF0000(INT32)
        check.INT32 (a, #FFFFFF05(INT32), "I32-109h")
      
      --}}} 
      --{{{  ><       (I32-110)
      a := 2 (INT32) >< b                  --  7
      a := (1 (INT32) >< b       ) + a     --  4
      a := (0 (INT32) >< b       ) + a     --  5
      a := ((-1 (INT32)) >< b    ) + a     -- -6
      a := ((-2 (INT32)) >< b    ) + a     -- -5
      a := (b >< 2 (INT32)       ) + a     --  7
      a := (b >< 1 (INT32)       ) + a     --  4
      a := (b >< 0 (INT32)       ) + a     --  5
      a := (b >< (-1 (INT32))    ) + a     -- -6
      a := (b >< (-2 (INT32))    ) + a     -- -5
      check.INT32 (a, 10 (INT32), "I32-110")
      a := b >< 0(INT32)
      check.INT32 (a, 5(INT32), "I32-110a")
      a := b >< (-1(INT32))
      check.INT32 (a, -6(INT32), "I32-110b")
      a := b >< b
      check.INT32 (a, 0(INT32), "I32-110c")
      INT32 bb :
      SEQ
        bb := id.INT32(#0101FF05(INT32))
        a := bb >< 0(INT32)
        check.INT32 (a, #0101FF05(INT32), "I32-110d")
        a := bb >< (-1(INT32))
        check.INT32 (a, #FEFE00FA(INT32), "I32-110e")
        a := bb >< #0101FF05(INT32)
        check.INT32 (a, 0(INT32), "I32-110f")
        a := bb >< #0000FFFF(INT32)
        check.INT32 (a, #010100FA(INT32), "I32-110g")
        a := bb >< #FFFF0000(INT32)
        check.INT32 (a, #FEFEFF05(INT32), "I32-110h")
      
      --}}} 
      --{{{  <<       (I32-111)
      a := 64 (INT32) << c                 --  512
      a := (32 (INT32) << c      ) + a     --  256
      a := (0 (INT32) << c       ) + a     --    0
      a := ((-64 (INT32)) << c   ) + a     -- -256
      a := ((-32 (INT32)) << c   ) + a     -- -512
      --a := (b << 64              ) + a     --    0
      a := (b << 32              ) + a     --    0
      a := (b << 16              ) + a     --   (5 << 16)
      a := (b << 8               ) + a     --   (5 << 8)
      a := (b << 0               ) + a     --    5
      --a := b << (-64) --should give error
      --a := b << (-32) --should give error
      check.INT32 (a, 5(INT32) + ((5(INT32) << 16) + (5(INT32) << 8)), "I32-111")
      --}}} 
      --{{{  >>       (I32-112)
      a := 64 (INT32) >> c                --  8
      a := (32 (INT32) >> c     ) + a     --  4
      a := (0 (INT32) >> c      ) + a     --  0
      a := ((-64 (INT32)) >> c  ) + a     --  (-64 >> 3)
      a := ((-32 (INT32)) >> c  ) + a     --  (-32 >> 3)
      --a := (b >> 64             ) + a     --  0
      a := (b >> 32             ) + a     --  0
      a := (b >> 16             ) + a     --  0
      a := (b >> 8              ) + a     --  0
      a := (b >> 0              ) + a     --  5
      --a := b >> (-64) --should give error
      --a := b >> (-32) --should give error
      VAL correct.result IS 17 (INT32) +
                 ( ( (-64(INT32)) >> 3) + ( (-32(INT32)) >> 3) ) :
      check.INT32 (a, correct.result, "I32-112")
      --}}} 
    --}}} 
    --{{{  check comparison operators              (I32-200 to 299)
    a := id.INT32(47(INT32))
    x := a = b
    check.BOOL (x, FALSE, "I32-205")
    x := a > b
    check.BOOL (x, FALSE, "I32-206")
    x := a >= b
    check.BOOL (x, FALSE, "I32-207")
    x := a < b
    check.BOOL (x, TRUE, "I32-208")
    x := a <= b
    check.BOOL (x, TRUE, "I32-209")
    x := a <> b
    check.BOOL (x, TRUE, "I32-210")
    x := a AFTER b
    check.BOOL (x, FALSE, "I32-211")
    
    --}}} 
    --{{{  check conversions                       (I32-300 to 399)
    --{{{  variables for conversion tests
    REAL64 r64 :
    REAL32 r32 :
    INT64 i64 :
    INT32 i32 :
    INT16 i16 :
    BYTE byte :
    BOOL bool :
    --}}} 
    SEQ
      r64 := REAL64 TRUNC b
      check.REAL64 (r64, REAL64 TRUNC 85(INT32), "I32-301")
      r64 := REAL64 TRUNC (b + 1(INT32))
      check.REAL64 (r64, REAL64 TRUNC 86(INT32), "I32-301a")
      r64 := REAL64 TRUNC (b+b)
      check.REAL64 (r64, REAL64 TRUNC 170(INT32), "I32-301b")
      r64 := REAL64 ROUND b
      check.REAL64 (r64, REAL64 ROUND 85(INT32), "I32-302")
      r64 := REAL64 ROUND (b + 1(INT32))
      check.REAL64 (r64, REAL64 ROUND 86(INT32), "I32-302a")
      r64 := REAL64 ROUND (b+b)
      check.REAL64 (r64, REAL64 ROUND 170(INT32), "I32-302b")
      r32 := REAL32 TRUNC b
      check.REAL32 (r32, REAL32 TRUNC 85(INT32), "I32-303")
      r32 := REAL32 TRUNC (b + 1(INT32))
      check.REAL32 (r32, REAL32 TRUNC 86(INT32), "I32-303a")
      r32 := REAL32 TRUNC (b+b)
      check.REAL32 (r32, REAL32 TRUNC 170(INT32), "I32-303b")
      r32 := REAL32 ROUND b
      check.REAL32 (r32, REAL32 ROUND 85(INT32), "I32-304")
      r32 := REAL32 ROUND (b + 1(INT32))
      check.REAL32 (r32, REAL32 ROUND 86(INT32), "I32-304a")
      r32 := REAL32 ROUND (b+b)
      check.REAL32 (r32, REAL32 ROUND 170(INT32), "I32-304b")
      i64 := INT64 b
      check.INT64 (i64, INT64 85(INT32), "I32-305")
      i32 := INT32 b
      check.INT32 (i32, INT32 85(INT32), "I32-306")
      i16 := INT16 b
      check.INT16 (i16, INT16 85(INT32), "I32-307")
      byte := BYTE b
      check.BYTE (byte, BYTE 85(INT32), "I32-308")
      c := 1(INT32)
      bool := BOOL c
      check.BOOL (bool, BOOL 1(INT32), "I32-309")
    --}}} 
    --{{{  check 'remainder identity'              (I32-400 to 499)
    a := ( 23(INT32) * ( b / 23(INT32) ) ) + ( b \ 23(INT32) )
    check.INT32 (a,
      ( 23(INT32) * ( 85(INT32) / 23(INT32) ) ) + ( 85(INT32) \ 23(INT32) ),
      "I32-400")
    check.INT32 (a, b, "I32-401")
    --}}} 
    --{{{  hefty calculations                      (I32-500 to 599)
    --{{{  declarations
    INT32 x, y, z, a, b, c, d :
    VAL n1 IS 1(INT32) :
    VAL n2 IS 2(INT32) :
    VAL n3 IS 3(INT32) :
    VAL n4 IS 1(INT32) / 3(INT32) :
    VAL eight.seven IS 2097152(INT32) : -- 8 to the power 7 (= 2 to the 21)
    --}}} 
    SEQ
      x := 8(INT32)
      y := x + x
      z := y - x
      a := 111111(INT32)
      b := 222222(INT32)
      c := a * 2(INT32)
      check.INT32 (y, 16(INT32), "I32-500")
      check.INT32 (z,  8(INT32), "I32-501")
      check.INT32 (c,  b, "I32-502")
      check.INT32 (((x + y) + (n3 * z)) + ((n2 * a) - b), 48(INT32), "I32-503")
      check.INT32 (((x PLUS y) PLUS (n3 TIMES z)) PLUS ((n2 TIMES a) MINUS b), 48(INT32), "I32-503a")
      check.INT32 (x + (x + (x + (x + (x + (x + x))))), 56(INT32), "I32-504")
      check.INT32 (x PLUS (x PLUS (x PLUS (x PLUS (x PLUS (x PLUS x))))), 56(INT32), "I32-504a")
      check.INT32 (x * (x * (x * (x * (x * (x * x))))), eight.seven, "I32-505")
      check.INT32 (x TIMES (x TIMES (x TIMES (x TIMES (x TIMES (x TIMES x))))), eight.seven, "I32-505a")
      check.INT32 (x * (x * (n2 * (y * (y * (x * x))))), eight.seven, "I32-506")
      check.INT32 (x TIMES (x TIMES (n2 TIMES (y TIMES (y TIMES (x TIMES x))))), eight.seven, "I32-506a")
      x := 1 (INT32)
      y := 3 (INT32)
      d := 2 (INT32)
      check.INT32 (x / y, n4, "I32-507")
      c := ((d * y) * (d * y)) * ((d * y) * (d * y))
      check.INT32 (c, 1296 (INT32), "I32-509")
      c := ((d TIMES y) TIMES (d TIMES y)) TIMES ((d TIMES y) TIMES (d TIMES y))
      check.INT32 (c, 1296 (INT32), "I32-509a")
      c := ((((d * y) * (d * y)) * (d * y)) * (d * y)) * (d * y)
      check.INT32 (c, 7776 (INT32), "I32-510")
      c := ((((d TIMES y) TIMES (d TIMES y)) TIMES (d TIMES y)) TIMES (d TIMES y)) TIMES (d TIMES y)
      check.INT32 (c, 7776 (INT32), "I32-510a")
    
    
    --}}} 
    --{{{  passing as parameters                   (I32-600 to 699)
    --{{{  copy.int32 and add.int32
    PROC copy.int32 (VAL INT32 x, INT32 y)
      y := x
    :
    PROC add.int32 (VAL INT32 x, y, INT32 z)
      z := x + y
    :
    --}}} 
    INT32 x, y, z :
    VAL n1 IS 2(INT32) :
    VAL n2 IS 500000(INT32) :
    SEQ
      copy.int32 (n1, x)
      check.INT32 (x, 2(INT32), "I32-600")
      copy.int32 (n2, x)
      check.INT32 (x, 500000(INT32), "I32-601")
      y := 57 (INT32)
      copy.int32 (y, x)
      check.INT32 (x, 57(INT32), "I32-602")
      add.int32 (n1, n2, x)
      check.INT32 (x, 500002(INT32), "I32-603")
      y := n1
      z := n2
      add.int32 (y, z, x)
      check.INT32 (x, 500002(INT32), "I32-604")
      check.INT32 (z, 500000(INT32), "I32-605")
    --}}} 
    --{{{  arrays                                  (I32-700 to 799)
    VAL n IS 1(INT32) :
    VAL m IS 2(INT32) :
    VAL t1 IS [n, n, n, n, m] :
    VAL t2 IS [t1, t1, t1, t1 ] :
    VAL t3 IS [t2, t2, t2 ] :
    BOOL b :
    INT x :
    INT32 tot :
    SEQ
      check.INT32 (t1 [0], n,         "I32-700")
      check.INT32 (t1 [4], m,         "I32-701")
      check.INT32 (t2 [2] [4], m,     "I32-702")
      check.INT32 (t3 [1] [2] [4], m, "I32-703")
      --{{{  test individual access
      b := TRUE
      x := 4
      tot := 0(INT32)
      SEQ i = 0 FOR SIZE t3
        SEQ j = 0 FOR SIZE t2
          SEQ k = 0 FOR SIZE t1
            SEQ
              IF
                (k = 4) AND (t3 [i] [j] [k] = m)
                  SKIP
                (k < 4) AND (t3 [i] [j] [k] = n)
                  SKIP
                TRUE
                  b := FALSE
              tot := tot + t3 [i] [j] [k]
              x := INT t3 [i] [j] [x]      -- sets it to 1
              x := 4                       -- resets it again
      check.BOOL (b, TRUE, "I32-704")
      VAL correct.value IS (48(INT32) * n) + (12(INT32) * m) :
      check.INT32 (tot, correct.value, "I32-705")
      --}}} 
      --{{{  test nested array access
      INT a, b, c :
      SEQ
        a := 2
        b := 1
        c := 3
        tot := t3[a][b][c] + ( t3[b][a][c] + ( t3[0][1][4] + ( t3[b][a+b][b+(a+b)] +
               t1[(a+b)+b] ) ) )
        VAL correct.value IS (2(INT32) * n) + (3(INT32) * m) :
        check.INT32 (tot, correct.value, "I32-706")
        tot := t3 [a] [ INT t3 [a] [b] [INT t3 [b + b] [a +
                        b] [b + (a + b)] ] ] [(INT t3 [a] [c] [c +
                        (b - a)]) +(3 * (INT t3 [a] [c] [b])) ]
        check.INT32 (tot, m, "I32-707")
      --}}} 
      --{{{  test individual access using abbreviations
      b := TRUE
      x := 4
      tot := 0(INT32)
      SEQ i = 0 FOR SIZE t3
        VAL a2 IS t3 [i] :
        SEQ j = 0 FOR SIZE t2
          VAL a1 IS a2 [j] :
          SEQ k = 0 FOR SIZE t1
            VAL a0 IS a1 [k] :
            SEQ
              IF
                (k = 4) AND (a0 = m)
                  SKIP
                (k < 4) AND (a0 = n)
                  SKIP
                TRUE
                  b := FALSE
              tot := tot + a0
              x := INT a1 [x]              -- sets it to 1
              x := 4                       -- resets it again
      check.BOOL (b, TRUE, "I32-708")
      VAL correct.value IS (48(INT32) * n) + (12(INT32) * m) :
      check.INT32 (tot, correct.value, "I32-709")
      --}}} 
      --{{{  test livermore loop                   (I32-710 to 711)
      PROC livermore()
        VAL n IS 10 :
        VAL r IS 7(INT32) :
        VAL t IS 13(INT32) :
        [n + 6] INT32 u :
        [n] INT32 x, y, z :
        SEQ
          SEQ k = 0 FOR n + 6
            u [k] := 3(INT32)
          SEQ k = 0 FOR n
            SEQ
              y [k] := 11(INT32)
              z [k] :=  5(INT32)
        
          SEQ k = 0 FOR n
            x [k] := u[k] + ((( r * (z[k] + (r * y[k]))) +
                              (t * ((u[k+3] + (r * (u[k+2] + (r * u[k+1]))))))) +
                              (t * ((u[k+6] + (r * (u[k+5] + (r * u[k+4])))))))
        
          check.INT32 (x[0],     5023(INT32), "I32-710")
          check.INT32 (x[n - 1], 5023(INT32), "I32-711")
      :
      livermore()
      --}}} 
      --{{{  test some bugs that were found        (I32-712 to 717)
      PROC bug.tests()
        SEQ
          --{{{  bug1                                  (I32-712)
          [2][2]INT32 arr :
          INT32 A :
          INT i :
          SEQ
            i := 0
            arr[i][i] := 0(INT32)
            A := ((arr[i][i]+arr[i][i]) + (arr[i][i]+arr[i][i])) / 4(INT32)
            check.INT32 (A, 0(INT32), "I32-712")
          --}}} 
          --{{{  bug3                                  (I32-713 to 714)
          PROC Complex.Subst ([]INT32 a, b)
            SEQ
              check.INT32 (a[0], 2(INT32), "I32-713")
              check.INT32 (b[1], 4(INT32), "I32-714")
          :
          
          [2][2][2]INT32 g.plus, g.minus :
          INT32 i, j :
          SEQ
            i := 1(INT32)
            j := 0(INT32)
            g.plus [INT i][INT j][0] := 2(INT32)
            g.minus[INT i][INT j][1] := 4(INT32)
            Complex.Subst (g.plus[INT i][INT j],
                           g.minus[INT i][INT j])
          --}}} 
          --{{{  bug4                                  (I32-715)
          [10][10]INT32 c, b :
          INT i :
          INT32 A:
          SEQ
            i := 1
            SEQ i = 0 FOR 10
              SEQ j = 0 FOR 10
                VAL tmp IS INT32 j :
                c [i][j] := tmp
            A := ((c[i][i] * c [i*i][i+2])) / (c[(i*i)+i][(i*i)+i] + c[i][i])
            check.INT32 (A, 1(INT32), "I32-715")
          --}}} 
          --{{{  test2                                 (I32-716 to 717)
          INT32 u :
          [4] INT32 ai :
          [3] [4] INT32 x, Gb :
          INT i, j, k :
          SEQ
            SEQ i = 0 FOR 4
              SEQ
                ai [i] := 5(INT32)
                SEQ j = 0 FOR 3
                  SEQ
                    x [j] [i] := 2(INT32)
                    Gb[j] [i] := 24(INT32)
            u := 4(INT32)
            i := 1
            j := 2
            k := 3
            u := u + (ai [j] * x [j][k])
            x [i][k] := (Gb [i][k] - u) / ai[i]
            check.INT32 (x [i][k], 2(INT32), "I32-716")
          
            u := 4(INT32)
            u := u PLUS (ai [j] TIMES x [j][k])
            x [i][k] := (Gb [i][k] MINUS u) / ai[i]
            check.INT32 (x [i][k], 2(INT32), "I32-717")
          
          --}}} 
      :
      bug.tests()
      --}}} 
    --}}} 
    --{{{  miscellaneous bug checks                (I32-800 to 899)
    INT32 x :
    SEQ
      x := 1(INT32)
      x := x << 17
      check.INT32 (x, 131072(INT32), "I32-800") -- was a T2 compiler bug
    --}}} 
    --{{{  more conversion tests
    INT FUNCTION id(VAL INT i) IS i :
    VAL n IS 5 :
    VAL m IS 7 :
    [n][m]INT64 i64arr :
    [n][m]INT32 i32arr :
    [n][m]INT16 i16arr :
    [n][m]INT   iarr :
    [n][m]REAL32 r32arr :
    [n][m]REAL64 r64arr :
    INT i, j :
    VAL ans IS (n / 2) + (m / 2) :
    SEQ
      i, j := n / 2, m / 2
      SEQ k = 0 FOR n
        SEQ l = 0 FOR m
          i32arr[k][l] := INT32(k + l)
    
      r64arr[i][j] := REAL64 ROUND i32arr[i][j]
      check.REAL64(r64arr[i][j], REAL64 ROUND ans, "I32-R64-1")
      r64arr[id(i)][j] := REAL64 ROUND i32arr[id(i)][j]
      check.REAL64(r64arr[i][j], REAL64 ROUND ans, "I32-R64-2")
      r64arr[i][j] := REAL64 TRUNC i32arr[i][j]
      check.REAL64(r64arr[i][j], REAL64 TRUNC ans, "I32-R64-3")
      r64arr[id(i)][j] := REAL64 TRUNC i32arr[id(i)][j]
      check.REAL64(r64arr[i][j], REAL64 TRUNC ans, "I32-R64-4")
    
      r32arr[i][j] := REAL32 ROUND i32arr[i][j]
      check.REAL32(r32arr[i][j], REAL32 ROUND ans, "I32-R32-1")
      r32arr[id(i)][j] := REAL32 ROUND i32arr[id(i)][j]
      check.REAL32(r32arr[i][j], REAL32 ROUND ans, "I32-R32-2")
      r32arr[i][j] := REAL32 TRUNC i32arr[i][j]
      check.REAL32(r32arr[i][j], REAL32 TRUNC ans, "I32-R32-3")
      r32arr[id(i)][j] := REAL32 TRUNC i32arr[id(i)][j]
      check.REAL32(r32arr[i][j], REAL32 TRUNC ans, "I32-R32-4")
    
      i64arr[i][j] := INT64 i32arr[i][j]
      check.INT64(i64arr[i][j], INT64 ans, "I32-I64-1")
      i64arr[id(i)][j] := INT64 i32arr[id(i)][j]
      check.INT64(i64arr[i][j], INT64 ans, "I32-I64-2")
    
      i32arr[i][j] := INT32 i32arr[i][j]
      check.INT32(i32arr[i][j], INT32 ans, "I32-I32-1")
      i32arr[id(i)][j] := INT32 i32arr[id(i)][j]
      check.INT32(i32arr[i][j], INT32 ans, "I32-I32-2")
    
      i16arr[i][j] := INT16 i32arr[i][j]
      check.INT16(i16arr[i][j], INT16 ans, "I32-I16-1")
      i16arr[id(i)][j] := INT16 i32arr[id(i)][j]
      check.INT16(i16arr[i][j], INT16 ans, "I32-I16-2")
    
      iarr[i][j] := INT i32arr[i][j]
      check.INT(iarr[i][j], INT ans, "I32-INT-1")
      iarr[id(i)][j] := INT i32arr[id(i)][j]
      check.INT(iarr[i][j], INT ans, "I32-INT-2")
    --}}}
    --{{{  more constant folding (bug INSdi02215, INSdi01895)
    PROC constant.TIMES(VAL INT32 y)
    
      VAL zero.zero IS          0(INT32) :
      VAL zero.one  IS          1(INT32) :
      VAL zero.any  IS          2(INT32) :
      VAL one.zero  IS #10000(INT32) :
      VAL one.one   IS #10001(INT32) :
      VAL one.any   IS #10002(INT32) :
      VAL any.zero  IS #20000(INT32) :
      VAL any.one   IS #20001(INT32) :
      VAL any.any   IS #20002(INT32) :
      VAL table IS [0(INT32), 1(INT32), 2(INT32), -1(INT32), -2(INT32),
                    MOSTNEG INT32, MOSTPOS INT32, 10(INT32),
                    zero.any,
                    one.zero, one.one, one.any,
                    any.zero, any.one, any.any,
                    -zero.any,
                    -one.zero, -one.one, -one.any,
                    -any.zero, -any.one, -any.any ] :
      SEQ
        SEQ z = 0 FOR SIZE table
          PROC local.check(VAL INT32 x, y, VAL []BYTE msg)
            check.INT32(x, y TIMES table[z], msg)
          :
          INT32 res, x :
          SEQ
            x := id.INT32(table[z])
    
            res := x TIMES zero.zero
            local.check(res, zero.zero, "INT32-a")
            res := x TIMES zero.one
            local.check(res, zero.one, "INT32-b")
            res := x TIMES zero.any
            local.check(res, zero.any, "INT32-c")
          
            res := x TIMES one.zero
            local.check(res, one.zero, "INT32-d")
            res := x TIMES one.one
            local.check(res, one.one, "INT32-e")
            res := x TIMES one.any
            local.check(res, one.any, "INT32-f")
          
            res := x TIMES any.zero
            local.check(res, any.zero, "INT32-g")
            res := x TIMES any.one
            local.check(res, any.one, "INT32-h")
            res := x TIMES any.any
            local.check(res, any.any, "INT32-i")
          
            res := zero.zero TIMES x
            local.check(res, zero.zero, "INT32-j")
            res := zero.one  TIMES x
            local.check(res, zero.one, "INT32-k")
            res := zero.any  TIMES x
            local.check(res, zero.any, "INT32-l")
          
            res := one.zero  TIMES x
            local.check(res, one.zero, "INT32-m")
            res := one.one   TIMES x
            local.check(res, one.one, "INT32-n")
            res := one.any   TIMES x
            local.check(res, one.any, "INT32-o")
          
            res := any.zero  TIMES x
            local.check(res, any.zero, "INT32-p")
            res := any.one   TIMES x
            local.check(res, any.one, "INT32-q")
            res := any.any   TIMES x
            local.check(res, any.any, "INT32-r")
    
            res := y         TIMES x
            local.check(res, y, "INT32-s")
    :
    constant.TIMES(27(INT32))
    --}}}
    --{{{  bug 334
    PROC bug.334()
      INT32 a, x, z :
      SEQ
        x, z := 99(INT32), -1(INT32)
        VAL y IS (INT32 x) :  -- null conversion
        SEQ
          a := y
          check.INT32(a, 99(INT32), "B334p")
          check.INT32(y, 99(INT32), "B334q")
        VAL y IS z * (INT32 x) :
        SEQ
          a := y
          check.INT32(a, -99(INT32), "B334r")
          check.INT32(y, -99(INT32), "B334s")
    :
    bug.334()
    --}}}
    --{{{  bug 670
    PROC bug.670()
      INT32 a,b :
      SEQ
        a := 2(INT32)
        b := (a + 3(INT32)) TIMES 3(INT32)
        check.INT32(b, 15(INT32), "B670a")
    :
    bug.670()
    --}}}
    --{{{  bug 1000
    PROC bug.1000()
      PROC q (VAL INT32 a0, a1, a2, a3, a4, INT32 b0, b1, b2, b3, b4)
        b0, b1, b2, b3, b4 := a0, a1, a2, a3, a4
      :
      [5]INT32 z0, z1 :
      SEQ
        SEQ i = 0 FOR SIZE z0
          z0[i], z1[i] := INT32 (-i), INT32 0
        q(z0[0],z0[1],z0[2],z0[3],z0[4],z1[0],z1[1],z1[2],z1[3],z1[4])
        SEQ i = 0 FOR SIZE z1
          check(INT z1[i], -i, "B1000a")
    :
    bug.1000()
    --}}}
    --{{{  bug 1150
    PROC bug.1150()
      INT32 x, y :
      VAL INT32 c IS   1(INT32) :
      VAL INT32 d IS 100(INT32) :
      SEQ
        x, y := c, d
        x := (x + x) + (y + y)
        check.INT32(x, (c+c)+(d+d), "B1150a")
        x, y := c, d
        x := (x + x) - (y + y)
        check.INT32(x, (c+c)-(d+d), "B1150b")
        x, y := c, d
        x := (x + x) + (x + x)
        check.INT32(x, (c+c)+(c+c), "B1150c")
        x, y := c, d
        x := (x + x) - (x + x)
        check.INT32(x, (c+c)-(c+c), "B1150d")
        x, y := c, d
        x := (y + y) + (x + x)
        check.INT32(x, (d+d)+(c+c), "B1150e")
        x, y := c, d
        x := (y + y) - (x + x)
        check.INT32(x, (d+d)-(c+c), "B1150f")
        x := #12345678(INT32)
        y := 1(INT32)
        x := (x /\ #000FFFFF(INT32))\/(((x>>20)+(y+1(INT32)))<<20)
        check.INT32(x, #12545678(INT32), "B1150g")
    :
    bug.1150()
    --}}}
    --{{{  bug 1274
    PROC bug.1274()
      INT32 x, y :
      VAL c IS 10(INT32) :
      VAL d IS 20(INT32) :
      SEQ
        y := c
    
        VAL a IS (c * c) + (c * d) :
        SEQ
          x := c
          x := (y * c) + (y * d)
          check.INT32(x, a, "B1274-3a+")
          x := c
          x := (x * c) + (y * d)
          check.INT32(x, a, "B1274-3b+")
          x := c
          x := (y * c) + (x * d)
          check.INT32(x, a, "B1274-3c+")
          x := c
          x := (x * c) + (x * d)
          check.INT32(x, a, "B1274-3d+")
    
        VAL a IS (c * c) PLUS (c * d) :
        SEQ
          x := c
          x := (y * c) PLUS (y * d)
          check.INT32(x, a, "B1274-3aP")
          x := c
          x := (x * c) PLUS (y * d)
          check.INT32(x, a, "B1274-3bP")
          x := c
          x := (y * c) PLUS (x * d)
          check.INT32(x, a, "B1274-3cP")
          x := c
          x := (x * c) PLUS (x * d)
          check.INT32(x, a, "B1274-3dP")
    
        VAL a IS (c * c) * (c * d) :
        SEQ
          x := c
          x := (y * c) * (y * d)
          check.INT32(x, a, "B1274-3a**")
          x := c
          x := (x * c) * (y * d)
          check.INT32(x, a, "B1274-3b**")
          x := c
          x := (y * c) * (x * d)
          check.INT32(x, a, "B1274-3c**")
          x := c
          x := (x * c) * (x * d)
          check.INT32(x, a, "B1274-3d**")
    
        VAL a IS (c * c) TIMES (c * d) :
        SEQ
          x := c
          x := (y * c) TIMES (y * d)
          check.INT32(x, a, "B1274-3aT")
          x := c
          x := (x * c) TIMES (y * d)
          check.INT32(x, a, "B1274-3bT")
          x := c
          x := (y * c) TIMES (x * d)
          check.INT32(x, a, "B1274-3cT")
          x := c
          x := (x * c) TIMES (x * d)
          check.INT32(x, a, "B1274-3dT")
    
    :
    bug.1274()
    --}}}
    --{{{  bug 1351
    PROC bug.1351()
      PROC p (VAL []INT buf)
        INT32 a, b :
        SEQ
          a,b := 1(INT32), 2(INT32)
          a := (INT32 (SIZE buf)) + (a + b)
          check.INT32(a, 4(INT32), "B1351a")
      :
      p ([55])
    :
    bug.1351()
    --}}}
    --{{{  bug 1333
    PROC bug.1333()
      INT32 x :
      BOOL b :
      SEQ
        x := 27(INT32)
        b := x < #FFFF8000(INT32)
        check(INT b, INT FALSE, "B1333a3")
        b := x < #FFFF8001(INT32)
        check(INT b, INT FALSE, "B1333b3")
        b := x < #FFFFFFFF(INT32)
        check(INT b, INT FALSE, "B1333c3")
        b := x < #FFFF7FFF(INT32)
        check(INT b, INT FALSE, "B1333d3")
        b := x < #8000(INT32)
        check(INT b, INT TRUE, "B1333e3")
        b := x < #8001(INT32)
        check(INT b, INT TRUE, "B1333f3")
        b := x < #FFFF(INT32)
        check(INT b, INT TRUE, "B1333g3")
        b := x < #7FFF(INT32)
        check(INT b, INT TRUE, "B1333h3")
    :
    bug.1333()
    --}}}
    --{{{  bug 1546
    PROC bug.1546c()
      INT32 x, y :
      INT places :
      SEQ
        x, places := ~#F(INT32), 2
        y := (x >> places) \/ ((- (INT32 (x < 0(INT32)))) << (32-places))
        check.INT32(y, ~#3(INT32), "B1546c")
    :
    bug.1546c()
    --}}}
    --{{{  bug TS/2020
    PROC bug.TS.2020()
      INT32 i32, mostneg, mostpos :
      SEQ
        i32 := (MOSTNEG INT32) REM (-1(INT32))
        check.INT32(i32, 0(INT32), "B2020ba")
        i32 := (MOSTNEG INT32) REM ( 1(INT32))
        check.INT32(i32, 0(INT32), "B2020bb")
        i32 := (MOSTPOS INT32) REM (-1(INT32))
        check.INT32(i32, 0(INT32), "B2020bc")
        i32 := (MOSTPOS INT32) REM ( 1(INT32))
        check.INT32(i32, 0(INT32), "B2020bd")
        i32 := (MOSTNEG INT32) /   ( 1(INT32))
        check.INT32(i32, MOSTNEG INT32, "B2020be")
        i32 := (MOSTPOS INT32) /   (-1(INT32))
        check.INT32(i32, (MOSTNEG INT32) + 1(INT32), "B2020bf")
        i32 := (MOSTPOS INT32) /   ( 1(INT32))
        check.INT32(i32, MOSTPOS INT32, "B2020bg")
    
        mostneg, mostpos := MOSTNEG INT32, MOSTPOS INT32
    
        i32 := (mostneg) REM (-1(INT32))
        check.INT32(i32, 0(INT32), "B2020bh")
        i32 := (mostneg) REM ( 1(INT32))
        check.INT32(i32, 0(INT32), "B2020bi")
        i32 := (mostpos) REM (-1(INT32))
        check.INT32(i32, 0(INT32), "B2020bj")
        i32 := (mostpos) REM ( 1(INT32))
        check.INT32(i32, 0(INT32), "B2020bk")
        i32 := (mostneg) /   ( 1(INT32))
        check.INT32(i32, MOSTNEG INT32, "B2020bl")
        i32 := (mostpos) /   (-1(INT32))
        check.INT32(i32, (MOSTNEG INT32) + 1(INT32), "B2020bm")
        i32 := (mostpos) /   ( 1(INT32))
        check.INT32(i32, MOSTPOS INT32, "B2020bn")
    :
    bug.TS.2020()
    --}}}
    --{{{  bug INSdi02195
    PROC bug.INSdi02195()
      --{{{  functions
      BOOL   FUNCTION BOOL.fn   (VAL INT x) IS BOOL  x :
      BYTE   FUNCTION BYTE.fn   (VAL INT x) IS BYTE  x :
      INT16  FUNCTION INT16.fn  (VAL INT x) IS INT16 x :
      INT    FUNCTION INT.fn    (VAL INT x) IS x :
      INT32  FUNCTION INT32.fn  (VAL INT x) IS INT32 x :
      INT64  FUNCTION INT64.fn  (VAL INT x) IS INT64 x :
      REAL32 FUNCTION REAL32.fn (VAL INT x) IS REAL32 ROUND x :
      REAL64 FUNCTION REAL64.fn (VAL INT x) IS REAL64 ROUND x :
      --}}}
      --{{{  variables
      BOOL bool.var :
      BYTE byte.var :
      INT16 i16.var :
      INT32 i32.var :
      INT64 i64.var :
      INT   int.var :
      REAL32 r32.var :
      REAL64 r64.var :
      INT zero, int :
      --}}}
      INT32 local.i, local.j, local.k :
      SEQ
        zero := id(0)
        int := id(77)
        --{{{  INT32 var -> type -> INT32         00 - 09
        local.j := id.INT32(1(INT32))
        
        local.i := INT32 (BOOL local.j)
        check.INT32(local.i, 1(INT32), "INT32-00")
        
        local.j := id.INT32(77(INT32))
        
        local.i := INT32 (BYTE local.j)
        check.INT32(local.i, 77(INT32), "INT32-01")
        local.i := INT32 (INT local.j)
        check.INT32(local.i, 77(INT32), "INT32-02")
        local.i := INT32 (INT16 local.j)
        check.INT32(local.i, 77(INT32), "INT32-03")
        local.i := INT32 (INT32 local.j)
        check.INT32(local.i, 77(INT32), "INT32-04")
        local.i := INT32 (INT64 local.j)
        check.INT32(local.i, 77(INT32), "INT32-05")
        local.i := INT32 ROUND (REAL32 ROUND local.j)
        check.INT32(local.i, 77(INT32), "INT32-06")
        local.i := INT32 ROUND (REAL64 ROUND local.j)
        check.INT32(local.i, 77(INT32), "INT32-07")
        --}}}
        --{{{  INT32 FUNCTION -> type -> INT32    10 - 19
        local.j := id.INT32(1(INT32))
        
        local.i := INT32 (BOOL INT32.fn(INT local.j))
        check.INT32(local.i, 1(INT32), "INT32-10")
        
        local.j := id.INT32(77(INT32))
        
        local.i := INT32 (BYTE INT32.fn(INT local.j))
        check.INT32(local.i, 77(INT32), "INT32-11")
        local.i := INT32 (INT INT32.fn(INT local.j))
        check.INT32(local.i, 77(INT32), "INT32-12")
        local.i := INT32 (INT16 INT32.fn(INT local.j))
        check.INT32(local.i, 77(INT32), "INT32-13")
        local.i := INT32 (INT32 INT32.fn(INT local.j))
        check.INT32(local.i, 77(INT32), "INT32-14")
        local.i := INT32 (INT64 INT32.fn(INT local.j))
        check.INT32(local.i, 77(INT32), "INT32-15")
        local.i := INT32 ROUND (REAL32 ROUND (INT32.fn(INT local.j)))
        check.INT32(local.i, 77(INT32), "INT32-16")
        local.i := INT32 ROUND (REAL64 ROUND (INT32.fn(INT local.j)))
        check.INT32(local.i, 77(INT32), "INT32-17")
        --}}}
        --{{{  INT32 exp -> type -> INT32         20 - 29
        local.k := id.INT32(0(INT32))
        
        local.j := id.INT32(1(INT32))
        
        local.i := INT32 (BOOL (local.j + local.k))
        check.INT32(local.i, 1(INT32), "INT32-20")
        
        local.j := id.INT32(77(INT32))
        
        local.i := INT32 (BYTE (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-21")
        local.i := INT32 (INT (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-22")
        local.i := INT32 (INT16 (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-23")
        local.i := INT32 (INT32 (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-24")
        local.i := INT32 (INT64 (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-25")
        local.i := INT32 ROUND (REAL32 ROUND (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-26")
        local.i := INT32 ROUND (REAL64 ROUND (local.j + local.k))
        check.INT32(local.i, 77(INT32), "INT32-27")
        --}}}
        --{{{  type var -> INT32                  30 - 39
        bool.var := BOOL id(1)
        local.i := INT32 bool.var
        check.INT32(local.i, 1(INT32), "INT32-30")
        byte.var := BYTE id(77)
        local.i := INT32 byte.var
        check.INT32(local.i, 77(INT32), "INT32-31")
        int.var := id.INT(77(INT))
        local.i := INT32 int.var
        check.INT32(local.i, 77(INT32), "INT32-32")
        i16.var := id.INT16(77(INT16))
        local.i := INT32 i16.var
        check.INT32(local.i, 77(INT32), "INT32-33")
        i32.var := id.INT32(77(INT32))
        local.i := INT32 i32.var
        check.INT32(local.i, 77(INT32), "INT32-34")
        i64.var := id.INT64(77(INT64))
        local.i := INT32 i64.var
        check.INT32(local.i, 77(INT32), "INT32-35")
        r32.var := id.REAL32(77.0(REAL32))
        local.i := INT32 ROUND r32.var
        check.INT32(local.i, 77(INT32), "INT32-36")
        r64.var := id.REAL64(77.0(REAL64))
        local.i := INT32 ROUND r64.var
        check.INT32(local.i, 77(INT32), "INT32-37")
        --}}}
        --{{{  type FUNCTION -> INT32             40 - 49
        local.i := INT32 BOOL.fn(zero)
        check.INT32(local.i, 0(INT32), "INT32-40")
        local.i := INT32 BYTE.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-41")
        local.i := INT32 INT.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-42")
        local.i := INT32 INT16.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-43")
        local.i := INT32 INT32.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-44")
        local.i := INT32 INT64.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-45")
        local.i := INT32 ROUND REAL32.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-46")
        local.i := INT32 ROUND REAL64.fn(int)
        check.INT32(local.i, 77(INT32), "INT32-47")
        --}}}
        --{{{  type exp -> INT32                  50 - 59
        bool.var := BOOL id(1)
        local.i := INT32 (bool.var = bool.var)
        check.INT32(local.i, 1(INT32), "INT32-50")
        byte.var := BYTE id(77)
        local.i := INT32 byte.var
        check.INT32(local.i, 77(INT32), "INT32-51")
        int.var := id.INT(77(INT))
        local.i := INT32 (int.var + (INT   zero))
        check.INT32(local.i, 77(INT32), "INT32-52")
        i16.var := id.INT16(77(INT16))
        local.i := INT32 (i16.var + (INT16 zero))
        check.INT32(local.i, 77(INT32), "INT32-53")
        i32.var := id.INT32(77(INT32))
        local.i := INT32 (i32.var + (INT32 zero))
        check.INT32(local.i, 77(INT32), "INT32-54")
        i64.var := id.INT64(77(INT64))
        local.i := INT32 (i64.var + (INT64 zero))
        check.INT32(local.i, 77(INT32), "INT32-55")
        r32.var := id.REAL32(77.0(REAL32))
        local.i := INT32 ROUND (r32.var + (REAL32 ROUND zero))
        check.INT32(local.i, 77(INT32), "INT32-56")
        r64.var := id.REAL64(77.0(REAL64))
        local.i := INT32 ROUND (r64.var + (REAL64 ROUND zero))
        check.INT32(local.i, 77(INT32), "INT32-57")
        --}}}
    :
    bug.INSdi02195()
    --}}}
    --{{{  bug INSdi02423
    PROC bug.INSdi02423()
    
      PROC CheckOverflow ( VAL INT32 address, INT32 result)
        result := address
      :  
    
      PROC local(VAL INT Areg, Breg, VAL INT32 expected0, expected1, VAL []BYTE msg)
        INT32 XBreg, XAreg, Xtimes :
        INT32 result0, result1 :
        SEQ
          CheckOverflow ( (INT32 Breg)TIMES(INT32 Areg), result0 )
          XBreg := (INT32 Breg)
          XAreg := (INT32 Areg)
          Xtimes := XAreg TIMES XBreg
          CheckOverflow (Xtimes, result1)
          check.INT32(result0, expected0, msg)
          check.INT32(result1, expected0, msg)
    
          CheckOverflow ( (INT32 Breg)*(INT32 Areg), result0 )
          Xtimes := XAreg * XBreg
          CheckOverflow (Xtimes, result1)
          check.INT32(result0, expected0, msg)
          check.INT32(result1, expected0, msg)
    
          CheckOverflow ( (INT32 Breg)+(INT32 Areg), result0 )
          Xtimes := XAreg + XBreg
          CheckOverflow (Xtimes, result1)
          check.INT32(result0, expected1, msg)
          check.INT32(result1, expected1, msg)
    
          CheckOverflow ( (INT32 Breg)PLUS(INT32 Areg), result0 )
          Xtimes := XAreg PLUS XBreg
          CheckOverflow (Xtimes, result1)
          check.INT32(result0, expected1, msg)
          check.INT32(result1, expected1, msg)
      :
      SEQ
        local(    1,     2,     2(INT32),  3(INT32), "di2423a")
        local(    2,     1,     2(INT32),  3(INT32), "di2423b")
        local(   -1,     2,    -2(INT32),  1(INT32), "di2423c")
        local(    1,    -2,    -2(INT32), -1(INT32), "di2423d")
        local(   -1,     1,    -1(INT32),  0(INT32), "di2423e")
        local(    1,    -1,    -1(INT32),  0(INT32), "di2423f")
        local(    1,     0,     0(INT32),  1(INT32), "di2423g")
        local(    0,     1,     0(INT32),  1(INT32), "di2423h")
        local(    0,    -1,     0(INT32), -1(INT32), "di2423i")
        local(   -1,     0,     0(INT32), -1(INT32), "di2423j")
    :
    bug.INSdi02423()
    --}}}
    --{{{  bug INSdi02439
    PROC bug.INSdi02439()
      SEQ
        PROTOCOL CONTROL  IS  INT; INT32 :
        CHAN OF CONTROL ControlToMonitor:
        PAR
          SEQ
            SEQ i = 0 FOR 4
              INT   ii :
              INT32 j :
              SEQ
                ControlToMonitor ? ii; j
                check(i, INT j, "di2439a")
                check(i, ii,    "di2439b")
          SEQ i = 0 FOR 4
            ControlToMonitor ! i; INT32 i
    :
    bug.INSdi02439()
    --}}}
    --{{{  bug INSdi03354
    PROC bug.INSdi03354()
      INT v1a, v1b :
      INT32 v2, v3 :
      SEQ
        v1a := id(835)
        v2 := (INT32 v1a) TIMES 100000(INT32)
        check.INT32(v2, 83500000(INT32), "di03354a")
    
        v1b := id(v1a)
        v3 := (INT32 v1b) * 100000(INT32)
        check.INT32(v3, 83500000(INT32), "di03354b")
    
        v2 := id.INT32(835(INT32)) TIMES 100000(INT32)
        check.INT32(v2, 83500000(INT32), "di03354c")
    
        v3 := id.INT32(835(INT32)) * 100000(INT32)
        check.INT32(v3, 83500000(INT32), "di03354d")
    :
    bug.INSdi03354()
    --}}}
:
do.INT32 ()
