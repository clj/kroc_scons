
#INCLUDE "course.module"
#INCLUDE "file.module"
#INCLUDE "convert.module"

VAL INT elements IS 256:

PROC element (CHAN INT this?, next!)
  INITIAL INT token IS 1:
  WHILE token <> 0
    SEQ
      this ? token
      IF
        token > 0
          next ! (token + 1)
        TRUE
          next ! token
:

PROC root (VAL INT cycles, tokens, CHAN INT this?, next!, CHAN BYTE scr!)
  INITIAL INT cycles IS cycles:
  INT token, sum:
  SEQ
    next ! 1
    this ? token
  
    out.string ("start*n", 0, scr!)
    scr ! FLUSH

    SEQ i = 1 FOR tokens
      next ! i

    WHILE cycles > 0
      SEQ
        SEQ i = 0 FOR tokens
          SEQ
            this ? token
            next ! (token + 1)
        cycles := cycles - 1

    sum := 0
    SEQ i = 0 FOR tokens
      SEQ
        this ? token
        sum := sum + token

    out.string ("end*n", 0, scr!)
    scr ! FLUSH

    out.int (sum, 0, scr!)
    scr ! '*n'

    next ! 0
    this ? token
:

PROC ring (CHAN BYTE kyb?, scr!, err!)
  INITIAL INT cycles IS 0:
  INITIAL INT tokens IS 1:
  SEQ
    CHAN GETOPT opts:
    PAR
      file.get.options ("", opts!)
      INITIAL INT arg.n IS 0:
      INITIAL BOOL done IS FALSE:
      WHILE NOT done
        opts ? CASE
          MOBILE []BYTE s:
          arg; s
            BOOL error:
            INT n:
            SEQ
              STRINGTOINT (error, n, s)
              ASSERT (NOT error)
              IF
                arg.n = 0
                  arg.n, cycles := arg.n + 1, n
                arg.n = 1
                  arg.n, tokens := arg.n + 1, n
                TRUE
                  SKIP
          done
            done := TRUE

    [elements]CHAN INT c:
    PAR
      PAR i = 1 FOR (elements - 1)
        element (c[i], c[(i + 1) \ (SIZE c)])
      root (cycles, tokens, c[0], c[1], scr!)
:
