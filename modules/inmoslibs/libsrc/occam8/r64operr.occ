
--
--	Inmos Libraries
--	Copyright (C) 1988, 1989, 1990, 1991, 1993  Inmos Limited
--
--	This library is free software; you can redistribute it and/or
--	modify it under the terms of the GNU Lesser General Public
--	License as published by the Free Software Foundation; either
--	version 2 of the License, or (at your option) any later version.
--
--	This library is distributed in the hope that it will be useful,
--	but WITHOUT ANY WARRANTY; without even the implied warranty of
--	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--	Lesser General Public License for more details.
--
--	You should have received a copy of the GNU Lesser General Public
--	License along with this library; if not, write to the Free Software
--	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
--

#IF (TARGET.BITS.PER.WORD = 32) AND (NOT DEFINED(TARGET.HAS.FPU))

--{{{  #options and #comments
#OPTION "E V"  -- no compiler libs, no vector space
#COMMENT "TA compiler library, V1.3 07/12/92"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

--{{{  #PRAGMA TRANSLATE
#PRAGMA TRANSLATE REAL64OPERR "REAL64OPERR%CHK"
--}}}

#INCLUDE "realvals.inc"

--{{{  
REAL64 FUNCTION REAL64OPERR (VAL REAL64 X, VAL INT Op, VAL REAL64 Yp)

  --{{{  history
  -- Beta-3 (11may87)
  -- V1.1, 21-Feb-89
  -- V1.2, 6-Apr-90 SRH
  -- Moved a CAUSEERROR to the start of a branch to allow compiler to
  -- do dead code elimination; V1.3 7-Dec-92 SRH
  --}}}

  REAL64 Result:
  VALOF
    #INCLUDE "nan32.inc"
    --{{{  #USE "occamutl.lib"
    #PRAGMA TRANSLATE RealIMul "RealIMul%CHK"
    #PRAGMA TRANSLATE RealIDiv "RealIDiv%CHK"
    #USE "occamutl.lib"  --  6-Apr-90
    --}}}
    --{{{  IEEE Double length constants for 32 bit implementation
    VAL DRealExpSh  IS        20 :
    VAL DRealInf    IS #7FF00000 :
    --}}}
    [2]INT Result RETYPES Result :
    VAL [2]INT X RETYPES X :
    VAL [2]INT Yp RETYPES Yp :
    INT Xexp, Yexp :
    [2] INT Xfrac :
    PLACE Xfrac IN WORKSPACE :
    [2] INT Yfrac :
    PLACE Yfrac IN WORKSPACE :
    INT Carry, Guard, Places :
    [2] INT Ans :
    PLACE Ans IN WORKSPACE :
    [2] INT Y :
    PLACE Y IN WORKSPACE :
    VAL INT Sign IS SignBit :
    SEQ
      --{{{  Unpack operands
      --{{{  Seperate mantissa and exponents
      IF
        Op = Sub              --get minus Y
          Y [1] := Yp [1] >< Sign
        TRUE                  --use plus Y
          Y [1] := Yp [1]
      Y [0] := Yp [0]
      
      Xfrac [1],  Xfrac [0] := SHIFTLEFT ( X [1],  X [0],  DRealShift)
      Yfrac [1],  Yfrac [0] := SHIFTLEFT ( Y [1],  Yp [0],  DRealShift)
      
      Xexp := (X [1] >> DRealExpSh) /\ DRealExp
      Yexp := (Y [1] >> DRealExpSh) /\ DRealExp
      --}}}
      
      Ans [0] := 0
      Carry := 0                          --assume not normal case
      IF
        (Xexp <> DRealExp) AND (Yexp <> DRealExp)
          --{{{  zeros or real nos
          IF
            (Xexp <> 0) AND (Yexp <> 0)
              --{{{  both normal nos
              SEQ                                  --normal case
                Carry := 1
                Xfrac [1] := Xfrac [1] \/ Sign
                Yfrac [1] := Yfrac [1] \/ Sign
              --}}}
            ((Xexp \/ Xfrac [1]) \/ Xfrac [0]) = 0
              --{{{  X is zero
              IF
                ((Yexp \/ Yfrac [1]) \/ Yfrac [0]) = 0--both zero
                  IF
                    Op < Mul                       --add/sub
                      SEQ
                        Ans [0] := 0
                        Ans [1] := X [1] /\ Y [1]
                    Op = Mul
                      Ans [1] := X [1] >< Y [1]    --signed zero
                    TRUE                           --div
                      SEQ
                        CAUSEERROR()
                        INT64 AnsNaN RETYPES Ans :
                        AnsNaN := ZeroZeroDivNaN.64
                Op < Mul                           --result is Y
                  SEQ
                    Ans [1] := Y [1]
                    Ans [0] := Y [0]
                TRUE                                 --mul or div
                  Ans [1] := (X [1] >< Y [1]) /\ Sign--zero
              --}}}
            ((Yexp \/ Yfrac [1]) \/ Yfrac [0]) = 0
              --{{{  Y is zero
              IF
                Op < Mul                           --add/sub
                  SEQ
                    Ans [1] := X [1]
                    Ans [0] := X [0]
                Op = Mul
                  Ans [1] := (X [1] >< Y [1]) /\ Sign--zero
                TRUE                               --div by zero
                  SEQ
                    CAUSEERROR()
                    Ans [1] := ((X [1] >< Y [1]) /\ Sign) \/ DRealInf--infinity
              --}}}
            TRUE
              --{{{  one or both are denormalised
              -- Put in implicit MSB
              -- Normalise any denormaliseds and correct their exponents if op is
              -- multiply or divide leave unormal for add and sub
              SEQ
                Carry := 1                         --work to do
                IF
                  Xexp <> 0                        --set leading bit
                    Xfrac [1] := Xfrac [1] \/ Sign
                  TRUE                             --set correct exponent
                    IF
                      Op > Sub
                        SEQ
                          Places,  Xfrac [1],  Xfrac [0] := NORMALISE ( Xfrac [1], Xfrac [0])
                          Xexp := 1 - Places
                      TRUE
                        Xexp := 1
                IF
                  Yexp <> 0
                    Yfrac [1] := Yfrac [1] \/ Sign
                  TRUE
                    IF
                      Op > Sub
                        SEQ
                          Places,  Yfrac [1],  Yfrac [0] := NORMALISE ( Yfrac [1], Yfrac [0])
                          Yexp := 1 - Places
                      TRUE
                        Yexp := 1
              --}}}
          --}}}
        TRUE
          --{{{  one or other inf/NaN
          SEQ
            CAUSEERROR()
            Xfrac [1] := Xfrac [1] /\ (~Sign)      --clear top bit
            Yfrac [1] := Yfrac [1] /\ (~Sign)
            IF
              (Xexp = DRealExp) AND (Yexp = DRealExp)
                --{{{  both inf/NaN
                IF
                  (((Xfrac [1] \/ Xfrac [0]) \/ Yfrac [1]) \/ Yfrac [0]) <> 0
                    SEQ
                      Guard, Places := LONGDIFF (Xfrac[0], Yfrac[0], 0)
                      Guard, Places := LONGDIFF (Xfrac[1], Yfrac[1], Guard)
                      IF
                        Places < 0
                          Ans := Y
                        TRUE
                          Ans := X
                      IF
                        Op < Mul
                          SKIP
                        TRUE
                          Ans [1] := (Ans [1] /\ (~Sign)) \/ ((X [1] >< Y [1]) /\ Sign)
                  (Op < Mul) AND (((X [1] >< Y [1]) /\ Sign) = 0)--add/sub same inf
                    Ans [1] := X [1]
                  Op = Mul
                    Ans [1] := ((X [1] >< Y [1]) /\ Sign) \/ DRealInf--infinity
                  TRUE                             --add/sub/div
                    INT64 AnsNaN RETYPES Ans :
                    IF
                      Op = Div
                        AnsNaN := InfInfDivNaN.64
                      TRUE
                        AnsNaN := AddSubInfInfNaN.64
                --}}}
              Xexp = DRealExp
                --{{{  X is inf/NaN
                IF
                  (Op < Mul)   -- inf +- finite  or  nan +- finite
                    Ans := X
                  (Xfrac [1] \/ Xfrac [0]) <> 0  -- nan */ finite
                    SEQ
                      Ans [0] := X [0]
                      Ans [1] := X [1] >< (Y [1] /\ Sign)
                  Op = Mul
                    IF
                      ((Yexp \/ Yfrac [1]) \/ Yfrac [0]) = 0--mul by zero
                        INT64 AnsNaN RETYPES Ans :
                        AnsNaN := ZeroInfMulNaN.64
                      TRUE                        --inf times normal
                        Ans [1] := ((X [1] >< Y [1]) /\ Sign) \/ DRealInf--infinity
                  TRUE
                    Ans [1] := ((X [1] >< Y [1]) /\ Sign) \/ DRealInf--infinity
                --}}}
              (Yfrac [1] \/ Yfrac [0]) <> 0
                --{{{  Y is NaN
                IF
                  (Op < Mul)    -- finite +- nan
                    Ans := Y
                  TRUE          -- finite */ nan
                    SEQ
                      Ans [1] := Y [1] >< (X [1] /\ Sign)     --keep NaN (may be - if sub)
                      Ans [0] := Y [0]
                --}}}
              TRUE
                --{{{  Y is inf
                IF
                  Op < Mul                        --add/sub
                    SEQ
                      Ans [1] := Y [1]
                      Ans [0] := Y [0]
                  Op = Mul
                    IF
                      ((Xexp \/ Xfrac [1]) \/ Xfrac [0]) = 0--mul by zero
                        INT64 AnsNaN RETYPES Ans :
                        AnsNaN := ZeroInfMulNaN.64
                      TRUE                        --inf times normal
                        Ans [1] := ((X [1] >< Y [1]) /\ Sign) \/ DRealInf--infinity
                  TRUE                            --div
                    Ans [1] := (X [1] >< Y [1]) /\ Sign
                --}}}
          --}}}
      --}}}
      --{{{  evaluate
      IF
        Carry = 0
          --{{{  Answer is set up
          SEQ
            Result [1] := Ans [1]
            Result [0] := Ans [0]
          --}}}
        TRUE
          --{{{  Do an operation
          SEQ
            IF
              --{{{  add or subtract
              Op < Mul                          --add/sub to do
                SEQ
                  Places := Xexp - Yexp
                  --{{{  Align Mantissas
                  IF
                    Places = 0
                      Guard := 0                -- correctly aligned
                    Places > 0
                      --{{{  shift Y
                      IF
                        Places > ((3 * BitsPerWord) - (DBitsInFrac + 1))
                          --{{{  shift Y over a long distance
                          IF
                            Places > (DBitsInFrac + 1)
                              --{{{  Total loss of significance
                              SEQ
                                Yfrac [1] := 0
                                Yfrac [0] := 0
                                Guard := 1      -- there was something in Yfrac as Y <> 0
                              --}}}
                            TRUE
                              --{{{  Save some significance
                              -- Some of the bits will be shifted out of the register triplet
                              -- and so need to be ORed into the guard/sticky register it is
                              -- safe to use the whole of the lower part of the fraction and
                              -- OR it if for a particular length machine, the above conditional
                              -- is true and
                              -- 2*BitsPerWord > DBitsInFrac ie 2*16 > 24 and 2*32 > 52,
                              SEQ
                                Carry := Yfrac [0]      -- Bits that might be lost
                                Yfrac [0],  Guard := SHIFTRIGHT ( Yfrac [1],  Yfrac [0],  Places - BitsPerWord)
                              
                                Guard := Guard \/ Carry
                                Yfrac [1] := 0
                              --}}}
                          --}}}
                        TRUE
                          --{{{  shift Y
                          SEQ
                            Carry,  Guard := SHIFTRIGHT ( Yfrac [0],  0,  Places)
                            IF
                              Places > BitsPerWord
                                SEQ
                                  Yexp,  Carry := SHIFTRIGHT ( Yfrac [1],  0,  Places - BitsPerWord)
                                  Guard := Guard \/ Carry
                              TRUE
                                SKIP
                            Yfrac [1],  Yfrac [0] := SHIFTRIGHT ( Yfrac [1],  Yfrac [0],  Places)
                          
                          --}}}
                      --}}}
                    TRUE
                      --{{{  shift X
                      IF
                        Places < (-((3 * BitsPerWord) - (DBitsInFrac + 1)))
                          --{{{  shift X over a long distance
                          SEQ
                            IF
                              Places < (-(DBitsInFrac + 1))
                                --{{{  Total loss of significance
                                SEQ
                                  Xfrac [1] := 0
                                  Xfrac [0] := 0
                                  Guard := 1      -- there was something in Xfrac as X <> 0
                                  Xexp := Yexp
                                --}}}
                              TRUE
                                --{{{  Save some significance
                                SEQ
                                  Carry := Xfrac [0]       -- Bits that might be lost
                                  Xfrac [0],  Guard := SHIFTRIGHT ( Xfrac [1],  Carry,  (-Places) - BitsPerWord)
                                  Guard := Guard \/ Carry
                                  Xfrac [1] := 0
                                  Xexp := Yexp
                                --}}}
                          --}}}
                        TRUE
                          --{{{  shift X
                          SEQ
                            Xexp := Yexp            --set largest exp
                            Carry,  Guard := SHIFTRIGHT ( Xfrac [0],  0,  -Places)
                            IF
                              Places < (-BitsPerWord)
                                SEQ
                                  Yexp,  Carry := SHIFTRIGHT ( Xfrac [1],  0,  (-Places) - BitsPerWord)
                                  Guard := Guard \/ Carry
                              TRUE
                                SKIP
                            Xfrac [1],  Xfrac [0] := SHIFTRIGHT ( Xfrac [1],  Xfrac [0],  -Places)
                          
                          --}}}
                      --}}}
                  --}}}
                  IF
                    ((X [1] >< Y [1]) /\ Sign) = 0
                      --{{{  add magnitudes
                      SEQ
                        -- Answer is always same sign as X and Y
                        Ans [1] := X [1] /\ Sign
                        Carry,  Xfrac [0] := LONGSUM ( Xfrac [0],  Yfrac [0],  0)
                        Carry,  Xfrac [1] := LONGSUM ( Xfrac [1],  Yfrac [1],  Carry)
                        IF
                          Carry <> 0                 --inc exponent
                            SEQ
                              Xexp := Xexp + 1       -- Overflow deals with itself
                              Carry,  Guard := SHIFTRIGHT ( Xfrac [0],  Guard,  1)
                      
                              Xfrac [1],  Xfrac [0] := SHIFTRIGHT ( Xfrac [1],  Xfrac [0], 1)
                          Xexp = 1              --check normalisation
                            IF
                              (Xfrac [1] /\ Sign) = 0
                                Xexp := 0       --still denormalised
                              TRUE
                                SKIP            --not denormalised
                          TRUE
                            SKIP
                      --}}}
                    TRUE
                      --{{{  sub magnitudes
                      SEQ
                        Carry,  Guard := LONGDIFF ( 0,  Guard,  0)
                        Places,  Ans [0] := LONGDIFF ( Xfrac [0],  Yfrac [0],  Carry)
                        Places,  Ans [1] := LONGDIFF ( Xfrac [1],  Yfrac [1],  Places)
                        -- Answer may have sign of either X or Y
                        -- dependant on their magnitudes
                        --{{{  Has sign change occured ?
                        IF
                          Places = 0                   --correct
                            SEQ
                              Xfrac [1] := Ans [1]
                              Xfrac [0] := Ans [0]
                              Ans [1] := X [1] /\ Sign -- Positive
                          TRUE                         --must negate
                            SEQ
                              Ans [1] := Y [1] /\ Sign --flip sign of answer
                              -- if sign is flipped we know they were almost aligned
                              -- so no borrow involved
                              Carry,  Xfrac [0] := LONGDIFF ( Yfrac [0],  Xfrac [0],  Carry)
                              Carry,  Xfrac [1] := LONGDIFF ( Yfrac [1],  Xfrac [1],  Carry)
                        --}}}
                        IF
                          (Xfrac [1] /\ Sign) <> 0     --normalised
                            SKIP
                          ((Xfrac [1] \/ Xfrac [0]) \/ Guard) = 0--result is zero
                            SEQ
                              Xexp := 0        -- +0
                              Ans [1] := 0
                          Xexp > 1
                            --{{{  not yet denormalised
                            SEQ
                              Places,  Xfrac [1],  Carry := NORMALISE ( Xfrac [1],  Xfrac [0])
                            
                              Xexp := Xexp - Places
                              IF
                                Xexp > 0            --still normal
                                  Xfrac [0],  Guard := SHIFTLEFT ( Xfrac [0],  Guard,  Places)
                            
                                TRUE                --gone denormalised
                                  SEQ
                                    Xfrac [1],  Carry := SHIFTRIGHT ( Xfrac [1],  0,  1 - Xexp)
                            
                                    Xfrac [0],  Guard := SHIFTLEFT ( Xfrac [0],  Guard,  Places - (1 - Xexp))
                            
                                    Xexp := 0
                            --}}}
                          TRUE                     --already denormalised
                            Xexp := 0
                      --}}}
              --}}}
              --{{{  multiply or division
              TRUE                                 --is mul/div
                SEQ
                  Ans [1] := (X [1] >< Y [1]) /\ Sign--get sign of answer
                  IF
                    --{{{  Mulitiply
                    Op = Mul                       --do multiply
                      SEQ
                        Xexp := (Xexp + Yexp) + (1 - DRealXcess)
                        Xfrac[1], Xfrac[0],  Guard := RealIMul ( Xfrac,  Yfrac)
                    --}}}
                    --{{{  Division
                    TRUE                           --do divide
                      SEQ
                        Xexp := (Xexp - Yexp) + DRealXcess
                        Xfrac [1],  Xfrac [0] := SHIFTRIGHT ( Xfrac [1],  Xfrac [0],  1)
                    
                        Guard := 0
                        Xfrac [1],  Xfrac [0],  Guard := RealIDiv (Xfrac [1],  Xfrac [0],  Guard,  Yfrac)
                        Carry := 0
                        Xfrac [0],  Guard,  Carry := RealIDiv (Xfrac [0],  Guard,  Carry,  Yfrac)
                        IF
                          Carry = 0
                            SKIP
                          TRUE                   --set sticky bits
                            Guard := Guard \/ 1
                    --}}}
                  --{{{  Normalise
                  Places,  Xfrac [1],  Carry := NORMALISE ( Xfrac [1],  Xfrac [0])
                  IF
                    Places > BitsPerWord
                      Xfrac [1] := Xfrac [1] \/ (Guard >> ((2 * BitsPerWord) - Places))
                    TRUE
                      SKIP
                  Xfrac [0],  Guard := SHIFTLEFT ( Xfrac [0],  Guard,  Places)
                  Xexp := Xexp - Places
                  --}}}
                  --{{{  Correct after normalisation
                  IF
                    --{{{  Overflow
                    Xexp >= DRealExp                --overflow
                      Xexp := DRealExp
                    --}}}
                    --{{{  Correct range
                    Xexp > 0                       --correct range
                      SKIP
                    --}}}
                    --{{{  Denormalised to insignificance
                    Xexp < (-(DBitsInFrac - 1))
                      SEQ
                        Xfrac [0] := 0
                        Xfrac [1] := 0
                        Guard := 1      -- cannot actually be zero so set guard for rounding
                        Xexp := 0
                    --}}}
                    --{{{  Denormalised
                    TRUE                           --denormalised
                      SEQ
                        Places := (Xfrac [0] /\ (DRealRBit \/ DRealXcess)) \/ Guard
                        IF
                          Xexp < (-(BitsPerWord - 2))
                            SEQ
                              Places := Places \/ Xfrac [0] -- Bug fix 42
                              Xfrac [0],  Guard := SHIFTRIGHT ( Xfrac [1],  Xfrac [0], (-Xexp) - (BitsPerWord - 1))
                              Xfrac [1] := 0
                          TRUE
                            SEQ
                              Carry,  Guard := SHIFTRIGHT ( Xfrac [0],  Guard,  1 - Xexp)
                              Xfrac [1],  Xfrac [0] := SHIFTRIGHT ( Xfrac [1],  Xfrac [0], 1 - Xexp)
                        Xexp := 0
                        Guard := Guard \/ Places
                    --}}}
                  --}}}
              --}}}
            --{{{  round & pack result
            IF
              Xexp < DRealExp                      --still in range
                --{{{  
                SEQ
                  Places := Xfrac [0]             --save round bits
                  Xfrac [1],  Xfrac [0] := SHIFTRIGHT ( Xfrac [1] /\ (~Sign), Xfrac [0],  DRealShift)
                  Carry,  Xexp := SHIFTRIGHT ( Xexp,  0,  DRealShift + 1)
                  Xfrac [1] := Xfrac [1] \/ Xexp
                  IF
                    (Places /\ DRealRBit) = 0      --round bit not set
                      SKIP
                    ((Guard \/ (Places /\ DRealXcess)) \/ (Xfrac [0] /\ 1)) = 0--round down
                      SKIP
                    TRUE                           --round up/even
                      SEQ
                        -- Overflow deals with itself
                        Carry,  Xfrac [0] := LONGSUM ( Xfrac [0],  1,  0)
                        Carry,  Xfrac [1] := LONGSUM ( Xfrac [1],  0,  Carry)
                  IF
                    (Xfrac [1] = DRealInf)   -- Overflow
                      CAUSEERROR()
                    TRUE
                      SKIP
                  Result [1] := Ans [1] \/ Xfrac [1]
                  Result [0] := Xfrac [0]
                --}}}
              TRUE                                 --OVERFLOW to infinity
                SEQ
                  CAUSEERROR()
                  Result [1] := Ans [1] \/ DRealInf
                  Result [0] := 0
            --}}}
          --}}}
      --}}}
    RESULT Result
:
--}}}

#ENDIF
