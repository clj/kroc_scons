--
--	Inmos Libraries
--	Copyright (C) 1988, 1989, 1990, 1991, 1993  Inmos Limited
--
--	This library is free software; you can redistribute it and/or
--	modify it under the terms of the GNU Lesser General Public
--	License as published by the Free Software Foundation; either
--	version 2 of the License, or (at your option) any later version.
--
--	This library is distributed in the hope that it will be useful,
--	but WITHOUT ANY WARRANTY; without even the implied warranty of
--	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--	Lesser General Public License for more details.
--
--	You should have received a copy of the GNU Lesser General Public
--	License along with this library; if not, write to the Free Software
--	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
--

#IF (TARGET.BITS.PER.WORD = 32) AND (NOT DEFINED(TARGET.HAS.FPU))

--{{{  #options and #comments
#OPTION "E V"  -- no compiler libs, no vector space
#COMMENT "TA compiler library, V1.2 07/04/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

--{{{  #PRAGMA TRANSLATE
#PRAGMA TRANSLATE REAL64TOINT64 "REAL64TOINT64%CHK"
--}}}

#INCLUDE "realvals.inc"

--{{{  REAL64TOINT64
INT64 FUNCTION REAL64TOINT64 (VAL INT Mode, VAL REAL64 X)

  -- Round real value double length integer (round to nearest or zero),
  -- returns an integer result. Error flag is set if an error occurs.
  --   ErrorFlag : 0 if ok, 1 if error
  --   N         : destination double integer
  --   Mode      : Truncate or Nearest (rounding mode)
  --   X         : source real
  -- Beta-2 (27feb87)

  -- from here to the end of the routine is almost identical to the idebug
  -- version -- a change in one should be reflected in the other

  INT64 N:
  VALOF
    --{{{  DEFs
    VAL INT REAL64MININT64.Hi IS #C3E00000 :
    --}}}
    [2]INT N RETYPES N :
    VAL [2]INT X RETYPES X :
    INT Carry, Dump, Guard, Xexp :
    [2] INT Xfrac :
    PLACE Xfrac IN WORKSPACE :
    INT Xsign :
    INT Sign :
    SEQ
      Sign := SignBit
      Xexp,  Dump := SHIFTLEFT ( 0,  X [1] /\ (~Sign),  DRealShift + 1)
      Xfrac [1],  Xfrac [0] := SHIFTLEFT ( X [1],  X [0],  DRealShift + 1)
      IF
        Xexp = DRealExp                    --not valid
          CAUSEERROR()
        (X [1] = REAL64MININT64.Hi) AND (X [0] = 0)  -- MinInt
          SEQ
            N [1] := Sign
            N [0] := 0
        Xexp < (DRealXcess - 1)            --less than half
          SEQ
            N [1] := 0
            N [0] := 0
        Xexp < DRealXcess                  --0.5 to 0.9*
          --{{{  
          IF
            (Mode = Truncate) OR ((Xfrac [1] \/ Xfrac [0]) = 0)
              SEQ
                N [1] := 0
                N [0] := 0
            (X [1] /\ Sign) = 0            --round up
              SEQ
                N [1] := 0
                N [0] := 1
            TRUE
              SEQ
                N [1] := -1
                N [0] := -1
          --}}}
        TRUE
          --{{{  
          SEQ
            Xsign := X [1] /\ Sign
            Xexp := Xexp - DRealXcess
            IF
              Xexp < BitsPerWord        --not past word shift
                SEQ
                  Carry := Xfrac [0]
                  Xfrac [0] := Xfrac [1]
                  Xfrac [1] := 1
                  Guard := 0
              TRUE
                SEQ
                  Xexp := Xexp - BitsPerWord
                  Carry := 0
                  Guard := 1               --set top bit
                  IF
                    Xexp > (BitsPerWord - 2)  --losing bits
                      CAUSEERROR()
                    TRUE
                      SKIP
            Guard,  Dump := SHIFTLEFT ( Guard,  Xfrac [1],  Xexp)
            Xfrac [1],  Dump := SHIFTLEFT ( Xfrac [1],  Xfrac [0],  Xexp)
            Xfrac [0],  Carry := SHIFTLEFT ( Xfrac [0],  Carry,  Xexp)
            IF
              (Mode = Truncate) OR ((Xfrac [0] /\ Sign) = 0)
                SKIP
              (((Xfrac [0] /\ (~Sign)) \/ Carry) \/ (Xfrac [1] /\ 1)) = 0--round down
                SKIP
              TRUE                           --round up/even
                SEQ
                  Carry,  Xfrac [1] := LONGSUM ( Xfrac [1],  1,  0)
                  Carry,  Guard := LONGSUM ( Guard,  0,  Carry)
            N [1] := Guard /\ (~Sign)        --dump excess top bits
            N [0] := Xfrac [1]
            IF
              Xsign = 0                      --positive
                SKIP
              TRUE
                SEQ
                  Carry,  N [0] := LONGDIFF ( 0,  N [0],  0)
                  Carry,  N [1] := LONGDIFF ( 0,  N [1],  Carry)
          --}}}
    RESULT N
:
--}}}

#ENDIF


#IF (TARGET.BITS.PER.WORD = 16)

--{{{  #options and #comments
#OPTION "E V"  -- no compiler libs, no vector space
#COMMENT "T2 compiler library, V1.2 07/04/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

--{{{  #PRAGMA TRANSLATE
#PRAGMA TRANSLATE REAL64TOINT64 "REAL64TOINT64%CHK"
--}}}

#INCLUDE "realvals.inc"

--{{{  REAL64TOINT64
INT64 FUNCTION REAL64TOINT64 (VAL INT Mode, VAL REAL64 X)

  -- Round real value quad length integer (round to nearest or zero),
  -- returns an integer result. Error flag is set if an error occurs.
  --   N         : destination double integer
  --   Mode      : Truncate or Nearest (rounding mode)
  --   X         : source real
  --{{{  history
  -- Function version (10 Jun 87)
  -- V1.1, 21-Feb-89
  -- V1.2, 7-Apr-90 SRH
  --}}}

  INT64 N :
  VALOF
    VAL [4]INT X RETYPES X :
    [4]INT N RETYPES N:
    INT Carry, Dump, Xexp :
    [4] INT Xfrac :
    PLACE Xfrac IN WORKSPACE :
    INT Xsign :
    INT Sign :
    SEQ
      Sign := SignBit
      --{{{  unpack
      Xexp,  Dump := SHIFTLEFT ( 0,  X[3] /\ (~Sign),  DRealShift + 1)
      Xfrac[3],  Dump := SHIFTLEFT ( X[3],  X[2],  DRealShift + 1)
      Xfrac[2],  Dump := SHIFTLEFT ( X[2],  X[1],  DRealShift + 1)
      Xfrac[1],  Xfrac[0] := SHIFTLEFT ( X[1],  X[0],  DRealShift + 1)
      --}}}
      IF
        --{{{  not valid -- Inf or NaN
        Xexp = DRealExp
          CAUSEERROR()
        --}}}
        --{{{  0   <= |X| < 0.5
        Xexp < (DRealXcess - 1)
          N := [0,0,0,0]
        --}}}
        --{{{  0.5 <= |X| < 1
        Xexp < DRealXcess
          IF
            (Mode = Truncate) OR ((((Xfrac[3] \/ Xfrac[2]) \/ Xfrac[1]) \/
              Xfrac[0]) = 0)                      --round to zero
              --exactly half
              N := [0,0,0,0]
            (X[3] /\ Sign) = 0             --round up
              N := [1,0,0,0]
            TRUE
              N := [-1,-1,-1,-1]
        --}}}
        --{{{  Proper case
        TRUE
          SEQ
            Xsign := X[3] /\ Sign
            Xexp := Xexp - DRealXcess
            IF
              --{{{  Most negative quad integer
              (Xsign <> 0) AND (Xexp = ((4 * BitsPerWord) - 1)) AND
                ((((Xfrac[0] \/ Xfrac[1]) \/ Xfrac[2]) \/ Xfrac[3]) = 0)
                N := [0,0,0,Sign]
              --}}}
              TRUE
                SEQ
                  --{{{  Do Shift
                  --{{{  Set up N, Xfrac for shift and set ErrorFlag if shift is too long
                  IF
                    Xexp < BitsPerWord        --not past word shift
                      N := [1,0,0,0]
                    Xexp < (2 * BitsPerWord)
                      SEQ
                        N := [Xfrac[3],1,0,0]
                        Xfrac[3] := Xfrac[2]
                        Xfrac[2] := Xfrac[1]
                        Xfrac[1] := Xfrac[0]
                        Xfrac[0] := 0
                        Xexp := Xexp - BitsPerWord
                    Xexp < (3 * BitsPerWord)
                      SEQ
                        N := [Xfrac[2],Xfrac[3],1,0]
                        Xfrac[3] := Xfrac[1]
                        Xfrac[2] := Xfrac[0]
                        Xfrac[1] := 0
                        Xfrac[0] := 0
                        Xexp := Xexp - (2 * BitsPerWord)
                    Xexp < ((4 * BitsPerWord) - 1)
                      SEQ
                        N := [Xfrac[1],Xfrac[2],Xfrac[3],1]
                        Xfrac[3] := Xfrac[0]
                        Xfrac[2] := 0
                        Xfrac[1] := 0
                        Xfrac[0] := 0
                        Xexp := Xexp - (3 * BitsPerWord)
                    TRUE
                      -- result = randomness
                      CAUSEERROR()
                  --}}}
                  N[3],  Dump := SHIFTLEFT ( N[3],  N[2],  Xexp)
                  N[2],  Dump := SHIFTLEFT ( N[2],  N[1],  Xexp)
                  N[1],  Dump := SHIFTLEFT ( N[1],  N[0],  Xexp)
                  N[0],  Xfrac[3] := SHIFTLEFT ( N[0],  Xfrac[3],  Xexp)
                  --}}}
                  --{{{  Round
                  IF
                    (Mode = Truncate) OR ((Xfrac[3] /\ Sign) = 0)--round to zero
                      --round bit clear
                      SKIP
                    (((((Xfrac[3] /\ (~Sign)) \/ Xfrac[2]) \/ Xfrac[1]) \/
                      Xfrac[0]) \/ (N[0] /\ 1)) = 0                --round down
                      SKIP
                    TRUE                           --round up/even
                      SEQ
                        Carry,  N[0] := LONGSUM ( N[0],  1,  0)
                        Carry,  N[1] := LONGSUM ( N[1],  0,  Carry)
                        Carry,  N[2] := LONGSUM ( N[2],  0,  Carry)
                        Carry,  N[3] := LONGSUM ( N[3],  0,  Carry)
                        IF
                          N[3] < 0
                            CAUSEERROR()
                          TRUE
                            SKIP
                  --}}}
                  N[3] := N[3] /\ (~Sign)         --dump excess top bits
                  --{{{  Set Sign
                  IF
                    Xsign = 0                      --positive
                      SKIP
                    TRUE
                      SEQ
                        Carry,  N[0] := LONGDIFF ( 0,  N[0],  0)
                        Carry,  N[1] := LONGDIFF ( 0,  N[1],  Carry)
                        Carry,  N[2] := LONGDIFF ( 0,  N[2],  Carry)
                        Carry,  N[3] := LONGDIFF ( 0,  N[3],  Carry)
                  --}}}
        --}}}
    RESULT N
:
--}}}

#ENDIF
