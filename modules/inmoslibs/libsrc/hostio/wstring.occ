--
--	Inmos Libraries
--	Copyright (C) 1988, 1989, 1990, 1991, 1993  Inmos Limited
--
--	This library is free software; you can redistribute it and/or
--	modify it under the terms of the GNU Lesser General Public
--	License as published by the Free Software Foundation; either
--	version 2 of the License, or (at your option) any later version.
--
--	This library is distributed in the hope that it will be useful,
--	but WITHOUT ANY WARRANTY; without even the implied warranty of
--	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--	Lesser General Public License for more details.
--
--	You should have received a copy of the GNU Lesser General Public
--	License along with this library; if not, write to the Free Software
--	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
--

--** @module hostio

#COMMENT "Host i/o library, V1.3, 06/02/91"
#COMMENT "(c) Copyright INMOS Limited 1989"
#INCLUDE "hostio.inc"
#USE "splib.lib"

--{{{  history
-- 09-JAN-90 SRH corrected so that so.write.string, so.fwrite.string,
-- so.write.string.nl and so.fwrite.string.nl place no restriction of the
-- length of string written. V1.1
-- 29-JUN-90 SRH changed the implementation of so.write.string.nl and
-- so.fwrite.string.nl so that the newline is not sent separately via
-- an sp.puts with null string ( "" ), but is appended by the server
-- via use of sp.puts with the last part of the string to send ( which
-- unless it is large will be the whole string ).  This makes these
-- routines appear to send a string and newline as an atomic unit, which
-- is the idea that their names convey. V1.2
-- 06-FEB-91 SRH took the 'f' routines out of this file (wstring.occ)
-- and put them into the file ( fwstring.occ ), so as to reduce the
-- code pulled into a program if only the spid.stdout stream is used
-- or equally if only streamid is used. V1.3
--}}}

--{{{  so.write.string
--* Write a string to standard out.
--
-- @param fs from server
-- @param ts to server
-- @param string the string to be written, which can be any length
PROC so.write.string (CHAN OF SP fs, ts, VAL []BYTE string)
  --{{{  more information
  --           If more than a packet's worth of data is to be written
  --           to standard out then one cannot guarantee that all of
  --           the data will be written before any other process
  --           accesses standard out.
  --}}}

  INT length : -- dummy in order to fill parameter slot
  so.write (fs, ts, spid.stdout, string, length)
:
--}}}
--{{{  so.write.char
--* Write a single character to standard out.
--
-- @param fs from server
-- @param ts to server
-- @param char the single character to write
PROC so.write.char (CHAN OF SP fs, ts, VAL BYTE char)
  BYTE result : -- dummy in order to fill parameter slot
  INT  length : -- dummy in order to fill parameter slot
  sp.write (fs, ts, spid.stdout, [char], length, result)
:
--}}}
--{{{  so.write.string.nl
--* Write a string appended by a newline to standard out.
--
-- The newline sequence is appended by this routine.
--
-- @param fs from server
-- @param ts to server
-- @param string the string to be written, which can be any length
PROC so.write.string.nl (CHAN OF SP fs, ts, VAL []BYTE string)
  --{{{  more information
  --           If more than a packet's worth of data is to be written
  --           to standard out then one cannot guarantee that all of
  --           the data will be written before any other process
  --           accesses standard out.
  -- The present implementation relies on the fact that the maximum
  -- size for the data that sp.write and sp.puts can handle is the
  -- same. 29/06/90 SRH
  --}}}

  BOOL writing :
  INT  length :
  BYTE result :  -- a dummy for this routine
  SEQ
    writing := TRUE
    length  := 0
    WHILE writing AND (length < ( (SIZE string) - sp.max.writebuffer.size ))
      INT length.written :
      SEQ
        sp.write (fs, ts, spid.stdout,
                  [string FROM length FOR sp.max.writebuffer.size],
                  length.written, result)
        writing := (length.written = sp.max.writebuffer.size)
        length  := length + length.written

    -- if writing is now FALSE then an error occurred whilst
    -- writing the string, so do not attempt further writing

    IF
      writing
        -- use sp.puts so as to have the newline appended directly to
        -- the end of the string by the server; the server also sees to
        -- the host dependency of the newline
        sp.puts (fs, ts, spid.stdout,
          [string FROM length FOR ((SIZE string) - length)], result)
      TRUE
        SKIP
:
--}}}
--{{{  so.write.nl
--* Write a newline to standard out.
--
-- @param fs from server
-- @param ts to server
PROC so.write.nl (CHAN OF SP fs, ts)
  BYTE result : -- dummy in order to fill parameter slot
  sp.puts (fs, ts, spid.stdout, "", result)
:
--}}}
