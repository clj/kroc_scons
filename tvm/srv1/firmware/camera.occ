#INCLUDE "srv1.inc"
#INCLUDE "camera_const.inc"

DATA TYPE CAMERA.MODE
  RECORD
    INT w:
    INT h:
    INT mode:
:

PROC camera (CAMERA? svr)
  VAL []CAMERA.MODE modes   IS [[ 160,  128, CAMERA.160.128  ],
                                [ 320,  256, CAMERA.320.256  ],
                                [ 640,  512, CAMERA.640.512  ],
                                [1280, 1024, CAMERA.1280.1024]]:
  
  BOOL, INT FUNCTION find.mode (VAL INT w, h)
    BOOL found:
    INT mode:
    VALOF
      IF
        IF i = 0 FOR SIZE modes
          VAL CAMERA.MODE m IS modes[i]:
          (m[w] = w) AND (m[h] = h)
            found, mode := TRUE, m[mode]
        TRUE
          found, mode := FALSE, CAMERA.STOP
      RESULT found, mode
  :

  CHAN INT camera.cfg:
  PLACE camera.cfg AT #2:

  CHAN INT camera.error:
  PLACE camera.error AT #2:

  CHAN MOBILE []BYTE camera.data:
  PLACE camera.data AT #2:

  INITIAL INT mode  IS CAMERA.STOP:
  INITIAL INT flags IS CAMERA.AUTO.ADJUST:
  SEQ
    INITIAL BOOL done IS FALSE:
    WHILE NOT done
      svr[req] ? CASE
        disconnected
          done := TRUE

        get.auto.adjust
          svr[rsp] ! auto.adjust; ((flags /\ CAMERA.AUTO.ADJUST) <> 0)

        get.frame
          IF
            mode <> CAMERA.STOP
              MOBILE []BYTE data:
              SEQ
                camera.data ? data
                IF
                  DEFINED data
                    -- No error; pass frame
                    svr[rsp] ! frame; data
                  TRUE
                    -- Frame empty; error
                    INT error:
                    SEQ
                      camera.error ? error
                      IF
                        error = 0
                          svr[rsp] ! error; ERROR.NO.MEMORY
                        TRUE
                          SEQ
                            svr[rsp] ! error; ERROR.HARDWARE
                      
                      mode := CAMERA.STOP
                      camera.cfg ! mode
            TRUE
              svr[rsp] ! error; ERROR.INV.STATE

        get.mode
          IF
            IF i = 0 FOR SIZE modes
              VAL CAMERA.MODE m IS modes[i]:
              m[mode] = mode
                IF
                  (flags /\ CAMERA.STREAMING) = 0
                    svr[rsp] ! capture.mode; m[w]; m[h]
                  TRUE
                    svr[rsp] ! stream.mode; m[w]; m[h]
            TRUE
              svr[rsp] ! error; ERROR.INV.STATE

        init
          SEQ
            mode := CAMERA.INIT
            camera.cfg ! mode
            mode := CAMERA.STOP

        BOOL on:
        set.auto.adjust; on
          SEQ
            IF
              on
                flags := flags \/ CAMERA.AUTO.ADJUST
              TRUE
                flags := flags /\ (~CAMERA.AUTO.ADJUST)
            IF
              mode <> CAMERA.STOP
                camera.cfg ! mode \/ flags
              TRUE
                SKIP
            svr[rsp] ! ok

        INT w, h:
        set.capture.mode; w; h
          BOOL found:
          INT new.mode:
          SEQ
            found, new.mode := find.mode (w, h)
            IF
              found
                SEQ
                  mode, flags := new.mode, flags /\ (~CAMERA.STREAMING)
                  camera.cfg ! mode \/ flags
                  svr[rsp] ! ok
              TRUE
                svr[rsp] ! error; ERROR.INV.PARAM

        INT w, h:
        set.stream.mode; w; h
          BOOL found:
          INT new.mode:
          SEQ
            found, new.mode := find.mode (w, h)
            IF
              found
                SEQ
                  mode, flags := new.mode, flags \/ CAMERA.STREAMING
                  camera.cfg ! mode \/ flags
                  svr[rsp] ! ok
              TRUE
                svr[rsp] ! error; ERROR.INV.PARAM

        stop
          SEQ
            mode := CAMERA.STOP
            camera.cfg ! mode
      
    IF
      mode <> CAMERA.STOP
        SEQ
          mode := CAMERA.STOP
          camera.cfg ! mode
      TRUE
        SKIP
:

