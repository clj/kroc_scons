#INCLUDE "defBF537.inc"
#INCLUDE "srv1.inc"

PROTOCOL P.LED
  CASE
    disconnected  = 0
    led           = 1; INT; BOOL
:

PROC leds (CHAN P.LED in?)
  PLACED [PORTGIO.LEN]INT16 port.g.io PORTGIO.ADDR:
  INITIAL [2]BOOL status IS [FALSE, FALSE]:
  SEQ
    -- Setup LED ports
    safe.set.register.16(port.g.io[PORTGIO.DIR], #0300, 0)
    -- Turn off LEDs
    safe.set.register.16(port.g.io[PORTGIO], #0300, 0)
    
    INITIAL BOOL done IS FALSE:
    WHILE NOT done
      INITIAL INT bits IS 0:
      SEQ
        in ? CASE
          disconnected
            done := TRUE
          INT i:
          BOOL b:
          led; i; b
            IF
              (i > 0) AND (i <= (SIZE status))
                status[i - 1] := b
              TRUE
                SKIP

        SEQ i = 0 FOR SIZE status
          IF
            status[i]
              bits := bits \/ (1 << (i + 8))
            TRUE
              SKIP
        
        safe.set.register.16(port.g.io[PORTGIO], bits >< #0300, bits)

    -- Turn off LEDs
    safe.set.register.16(port.g.io[PORTGIO], #0300, 0)
:

